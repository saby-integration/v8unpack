{4,
{49,0,0,0,0,1,0,0,00000000-0000-0000-0000-000000000000,1,
{1,0},0,0,1,1,1,0,1,0,
{0,1,0},
{0},1,
{22,
{-1,02023637-7868-4a5f-8576-835a76e0c9ba},0,0,1,
{0,
{0,
{"B",1},0}
},9,"ФормаКоманднаяПанель",
{1,0},
{1,0},0,1,0,0,0,2,2,
{3,4,
{0}
},
{7,3,0,1,100},
{0,0,0},1,
{0,0,1},0,1,0,0,0,3,3,0},0,"","",1,
{22,
{0},0,0,0,7,"Navigator",
{1,0},
{1,0},0,1,0,0,0,2,2,
{3,4,
{0}
},
{7,3,0,1,100},
{0,0,0},0,0,1,0,1,
{12,
{0},0,0,0,0,"NavigatorРасширеннаяПодсказка",
{1,0},
{1,0},1,0,0,2,2,
{3,4,
{0}
},
{7,3,0,1,100},
{0,0,0},1,
{5,0,0,3,0,
{0,1,0},
{3,4,
{0}
},
{3,4,
{0}
},
{3,0,
{0},0,1,0,48312c09-257f-4b29-b280-284dd89efc1e}
},0,1,2,
{1,
{1,0},0},0,0,1,0,0,1,0,3,3,0,0},0,3,3,0},1,"",2,0,0,0,0,0,3,3,0,0,2,100,1,1,0,0,0,
{49,0}
},"
&НаКлиенте
Перем МестныйКэш Экспорт;
&НаКлиенте
Перем ЕстьИзменения Экспорт;
&НаКлиенте
Перем ПоследнееИзменение Экспорт;
// функции для совместимости кода 
&НаКлиенте
Функция сбисПолучитьФорму(ИмяФормы)
	Если ТипЗнч(ЭтаФорма) = Тип(""УправляемаяФорма"") Тогда
		Попытка
			ЭтотОбъект="""";
		Исключение
		КонецПопытки;
		Возврат ПолучитьФорму(""ВнешняяОбработка.СБИС.Форма.""+ИмяФормы);
	КонецЕсли;
	Возврат ЭтотОбъект.ПолучитьФорму(ИмяФормы);
КонецФункции
&НаКлиенте
Функция сбисЭлементФормы(Форма,ИмяЭлемента)
	Если ТипЗнч(ЭтаФорма) = Тип(""УправляемаяФорма"") Тогда
		Возврат Форма.Элементы.Найти(ИмяЭлемента);
	КонецЕсли;
	Возврат Форма.ЭлементыФормы.Найти(ИмяЭлемента);
КонецФункции
&НаКлиенте
Функция сбисПолучитьСтраницу(Элемент, ИмяСтраницы)
	Если ТипЗнч(ЭтаФорма) = Тип(""УправляемаяФорма"") Тогда
		Возврат Элемент.ПодчиненныеЭлементы[ИмяСтраницы];
	КонецЕсли;
	Возврат Элемент.Страницы[ИмяСтраницы];
КонецФункции
&НаКлиенте
Процедура сбисПоказатьСостояние(ТекстСостояния, Форма = Неопределено, Индикатор = Неопределено, Пояснение = """")
	Если ТипЗнч(ЭтаФорма) = Тип(""УправляемаяФорма"") Тогда
		Состояние(ТекстСостояния,Индикатор,Пояснение);
	Иначе
		Форма.ЭлементыФормы.ПанельОжидания.Видимость = Истина;
		Форма.НадписьОжидания = Символы.ПС + ТекстСостояния;
		Форма.НадписьПояснение = Пояснение;
		Если Индикатор<>Неопределено Тогда
			Форма.ЭлементыФормы.Индикатор.Видимость = Истина;
			Форма.ЭлементыФормы.Индикатор.Значение = Индикатор;
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры
&НаКлиенте
Процедура сбисСпрятатьСостояние(Форма = Неопределено)
	Если ТипЗнч(ЭтаФорма) = Тип(""УправляемаяФорма"") Тогда
	Иначе
		Форма.ЭлементыФормы.ПанельОжидания.Видимость = Ложь;
		Форма.ЭлементыФормы.Индикатор.Видимость = Ложь;
	КонецЕсли;	
КонецПроцедуры
&НаКлиенте
Функция сбисСообщитьОбОшибке(ИнформацияОПакете = """") Экспорт
	Ошибка = ПолучитьСообщениеОбОшибке(Ложь);
	Если Не МестныйКэш.ТихийРежим Тогда
		Если ТипЗнч(ЭтаФорма) = Тип(""УправляемаяФорма"") Тогда
			Попытка
				ЭтотОбъект="""";
			Исключение
			КонецПопытки;
			Сообщить(ИнформацияОПакете + Ошибка.ПолныйТекст);
		Иначе
			фрм =ЭтотОбъект.ПолучитьФорму(""ФормаОшибка"");
			фрм.ТекстОшибки = Ошибка.ТекстОшибки;
			фрм.ИнформацияОбОшибке = ИнформацияОПакете + Ошибка.ИнформацияОбОшибке;
			фрм.ОткрытьМодально(60);
		КонецЕсли;
	КонецЕсли;
	Возврат Ошибка.ТекстОшибки;
КонецФункции

//------------------------------------------------------

//////////////////// Функции SDK //////////////////////
&НаКлиенте
Функция ВключитьОтладку(Кэш, КаталогОтладки) Экспорт
	// Включает логирование	
	Если Не Кэш.СБИС.ОбменВключен Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		КаталогЛогов = Новый Файл(КаталогОтладки);
		Если Не КаталогЛогов.Существует() Тогда
			СоздатьКаталог(КаталогОтладки);
		КонецЕсли;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	SimpleObject =  Кэш.Docflow.CreateSimpleObject();
	SimpleObject.Write(""Папка"", КаталогОтладки);
	SimpleObject.Write(""Уровень"", 9);
	
	SbisLog = Новый COMОбъект(""Sbis.Log"");
	Результат = SbisLog.WriteLogInfo(SimpleObject);
	Если Результат = 0 Тогда
		КаталогОтладки = Кэш.Парам.КаталогОтладки;
		Сообщить(SbisLog.ReadLastError());	
		Сообщить(""Для изменения каталога снимите галку """"Вести протоколирование"""", перезапустите 1C Предприятие, включите протоколирование и укажите новый каталог."");	
	КонецЕсли;
КонецФункции
&НаКлиенте
Функция ОтключитьОтладку(Кэш) Экспорт
	// Отключает логирование	
	Если Не Кэш.СБИС.ОбменВключен Тогда
		Возврат Ложь;
	КонецЕсли;
	
	SimpleObject =  Кэш.Docflow.CreateSimpleObject();
	SimpleObject.Write(""Уровень"", 0);
	SbisLog = Новый COMОбъект(""Sbis.Log"");
	Результат = SbisLog.WriteLogInfo(SimpleObject);
КонецФункции
// Изменяет каталог отладки с соответствующими проверками
&НаКлиенте
Функция УстановитьКаталогОтладки(Кэш) Экспорт
	ГлавноеОкно = Кэш.ГлавноеОкно;
	КаталогДоИзменения = Кэш.Парам.КаталогОтладки;
	
	// Если никакой каталог не задан и мы поставили галку, то открывать выбор каталога
	Если	ГлавноеОкно.РежимОтладки Тогда
		Если ГлавноеОкно.РежимОтладки <> Кэш.Парам.РежимОтладки Тогда
			РежимВыбора = Истина;
			КаталогТест = ГлавноеОкно.КаталогОтладки;
			ГлавноеОкно.КаталогОтладки = """";
		ИначеЕсли КаталогДоИзменения <> ГлавноеОкно.КаталогОтладки Тогда
			РежимВыбора = Истина;
		Иначе
			РежимВыбора = Ложь;
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		КаталогИзменен = УстановитьКаталогОтладкиРекурсивно(ГлавноеОкно, КаталогДоИзменения, РежимВыбора, КаталогТест);	
	Исключение
		СбисИсключение = Кэш.ОбщиеФункции.СбисИсключение(, ""ИнтеграцияКаталог.УстановитьКаталогОтладкиРекурсивно"", 772, ""Ошибка работы с файловой системой"", ОписаниеОшибки());
		ГлавноеОкно.СбисСообщитьОбОшибке(Кэш, СбисИсключение);
		ГлавноеОкно.КаталогОтладки = КаталогДоИзменения;
		Возврат Ложь;
	КонецПопытки;
	
	Возврат КаталогИзменен;
КонецФункции
&НаКлиенте
Функция УстановитьКаталогОтладкиРекурсивно(ГлавноеОкно, КаталогДоИзменения, РежимВыбора, КаталогТест = """")
	Если ГлавноеОкно.КаталогОтладки = """" Тогда
		Если РежимВыбора Тогда
			ОтказОтВыбора = Ложь;
			л_КаталогОтладки = ГлавноеОкно.сбисВыбратьКаталог(КаталогТест, ОтказОтВыбора);
			Если ОтказОтВыбора Тогда
				ГлавноеОкно.РежимОтладки = Ложь;
				Возврат Истина;
			КонецЕсли;
			ГлавноеОкно.КаталогОтладки = л_КаталогОтладки;
			УстановитьКаталогОтладкиРекурсивно(ГлавноеОкно, КаталогДоИзменения, РежимВыбора, КаталогТест);
		Иначе
			ГлавноеОкно.РежимОтладки = Ложь;
		КонецЕсли;
		Возврат ГлавноеОкно.КаталогОтладки <> КаталогДоИзменения;
		//Каталог менялся руками, проверить что такой есть
	Иначе
		л_КаталогОтладки = ГлавноеОкно.сбисФорматКаталога(ГлавноеОкно.КаталогОтладки);
		Файл = Новый Файл(ГлавноеОкно.КаталогОтладки);
		Если Не Файл.Существует() Или Не Файл.ЭтоКаталог() Тогда
			КаталогТест	= ГлавноеОкно.КаталогОтладки;
			РежимВыбора	= Истина;
			ГлавноеОкно.КаталогОтладки = """";
			УстановитьКаталогОтладкиРекурсивно(ГлавноеОкно, КаталогДоИзменения, РежимВыбора, КаталогТест);
			Возврат ГлавноеОкно.КаталогОтладки <> КаталогДоИзменения;
		КонецЕсли;
		ГлавноеОкно.КаталогОтладки = л_КаталогОтладки;
		Возврат ГлавноеОкно.КаталогОтладки <> КаталогДоИзменения;
	КонецЕсли;
КонецФункции
&НаКлиенте
Функция ЗакрытьСессию(Кэш) Экспорт 	
	// Закрывает сессию	
	Кэш.СБИС.Авторизован = Ложь;
	Возврат Кэш.Docflow.TerminateSession();
КонецФункции	
&НаКлиенте
Функция СформироватьНастройкиПодключения(Кэш, ИдентификаторСессии = """", ВремяОжидания = 60000) Экспорт
	// Устанавливает в SDK настройки подключения		
	СтрокаВерсии = ""1C"" + СтрЗаменить(Лев(Кэш.ПараметрыСистемы.Клиент.ВерсияПриложения, 3), ""."", """");
	СтрокаВерсии = СтрокаВерсии + ""/"" + Кэш.ПараметрыСистемы.Обработка.Версия; 	
	ConnectionInfo = Кэш.Docflow.CreateSimpleObject();
	ConnectionInfo.Write(""АдресСервера"", Кэш.СБИС.АдресСервера);
	ConnectionInfo.Write(""ИнформацияОПриложении"",	СтрокаВерсии);
	ConnectionInfo.Write(""ТаймаутСервера"",	ВремяОжидания);
	Если Кэш.Парам.ТипПрокси = ""Вручную"" Тогда
		ConnectionInfo.Write(""ОпределятьПроксиАвтоматически"", ""Нет"");
		ConnectionInfo.Write(""АдресПроксиСервера"", Кэш.Парам.ПроксиСервер+"":""+МестныйКэш.Парам.ПроксиПорт);
		ConnectionInfo.Write(""ЛогинПроксиСервера"", Кэш.Парам.ПроксиЛогин);
		ConnectionInfo.Write(""ПарольПроксиСервера"", Кэш.Парам.ПроксиПароль);
		
	ИначеЕсли Кэш.Парам.ТипПрокси = ""Автоматически"" Тогда 
		ConnectionInfo.Write(""ОпределятьПроксиАвтоматически"", ""Да"");
		
	ИначеЕсли Кэш.Парам.ТипПрокси = ""НеИспользовать"" Тогда 
		ConnectionInfo.Write(""ОпределятьПроксиАвтоматически"", ""Нет"");
	КонецЕсли;
	Если ЗначениеЗаполнено(ИдентификаторСессии) Тогда
		ConnectionInfo.Write(""ИдентификаторСессии"", ИдентификаторСессии);	
	КонецЕсли;
	ConnectionInfo.Write(""КоличествоПопытокЗагрузки"", 10); 
	ConnectionInfo.Write(""ТаймаутПопытокЗагрузки"", 1000);	
	Кэш.Docflow.WriteConnectionInfo(ConnectionInfo);
КонецФункции
&НаКлиенте
Функция сбисУстановитьВремяОжидания(Кэш, ВремяОжидания) Экспорт
	СформироватьНастройкиПодключения(Кэш, Кэш.Парам.ИдентификаторСессии, ВремяОжидания*1000);
КонецФункции
&НаКлиенте
Функция ПолучитьИдентификаторСессии(Кэш) Экспорт
	// получает идентификатор текущей сессии	
	ConnectionInfo = Кэш.Docflow.ReadConnectionInfo();
	ИдентификаторСессии = ConnectionInfo.Read(""ИдентификаторСессии"");
	Возврат ИдентификаторСессии;
КонецФункции
&НаКлиенте
Функция ПолучитьСписокСертификатовДляАвторизации(Кэш,ТекстОшибки) Экспорт
	// Получает список сертификатов для авторизации	
	МестныйКэш = Кэш;
	СформироватьНастройкиПодключения(Кэш);
	СписокСертификатов = Новый СписокЗначений();
	
	Object = Кэш.docflow.ReadCertificatesForAuth(МестныйКэш.Docflow.CreateSimpleObject());
	Если Object=Неопределено Тогда
		ТекстОшибки = ПолучитьСообщениеОбОшибке();
		Возврат СписокСертификатов;
	КонецЕсли;
	ObjectList = Object.ReadObjectList(""Сертификат"");
	Если ObjectList<>Неопределено Тогда
		Размер = ObjectList.count;
		Для сч=0 По Размер-1 Цикл
			Сертификат = СериализоватьObjectВСтруктуру(ObjectList.at(сч));
			Если Сертификат.Аутентификация.Зарегистрирован = ""Да"" Тогда
				Если Сертификат.Свойство(""ДействителенПо"") Тогда
					Если ТипЗнч(Сертификат.ДействителенПо)=Тип(""Дата"") Тогда
						ДействителенПо = Дата(Год(Сертификат.ДействителенПо), Месяц(Сертификат.ДействителенПо), День(Сертификат.ДействителенПо));
					Иначе
						ДействителенПо = Дата(Сред(Сертификат.ДействителенПо,7,4), Сред(Сертификат.ДействителенПо,4,2), Лев(Сертификат.ДействителенПо, 2));
					КонецЕсли;
				КонецЕсли;
				СписокСертификатов.Добавить(Сертификат, Кэш.ОбщиеФункции.ПолучитьПредставлениеСертификата(Сертификат, ""[ФИО].([Должность])., [Название].(ИНН [ИНН])., действителен до [ДействителенПо]"", Истина, ""ДЛФ=DD""));
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Возврат СписокСертификатов;
КонецФункции
&НаКлиенте
Функция ПолучитьСписокСертификатов(Кэш, filter=Неопределено) Экспорт
	// Получает список доступных сертификатов	
	МестныйКэш = Кэш;
	СписокСертификатов = Новый СписокЗначений();
	
	Если filter=Неопределено Тогда
		filter = МестныйКэш.Docflow.CreateSimpleObject();
	КонецЕсли;
	Object = Кэш.docflow.ReadCertificates(filter);
	Если Object=Неопределено Тогда
		Возврат СписокСертификатов;
	КонецЕсли;
	ObjectList = Object.ReadObjectList(""Сертификат"");
	Если ObjectList<>Неопределено Тогда
		Размер = ObjectList.count();
		Для сч=0 По Размер-1 Цикл
			Сертификат = СериализоватьObjectВСтруктуру(ObjectList.at(сч));
			Если ТипЗнч(Сертификат.ДействителенПо)=Тип(""Дата"") Тогда
				ДействителенПо = Дата(Год(Сертификат.ДействителенПо), Месяц(Сертификат.ДействителенПо), День(Сертификат.ДействителенПо));
			Иначе
				ДействителенПо = Дата(Сред(Сертификат.ДействителенПо,7,4), Сред(Сертификат.ДействителенПо,4,2), Лев(Сертификат.ДействителенПо, 2));
			КонецЕсли;
			Если ДействителенПо>=ТекущаяДата() Тогда
				СписокСертификатов.Добавить(Сертификат);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат СписокСертификатов;
КонецФункции
&НаКлиенте
функция ПолучитьСертификатыДляАктивации(Кэш, СписокИНН) Экспорт
	// функция активирует серверные сертификаты для определенного списка ИНН	
	СписокСертификатовДляАктивации = Новый СписокЗначений;;
	СписокСертификатов = ПолучитьСписокСертификатов(Кэш);
	Для Каждого ИНН Из СписокИНН Цикл
		Для Каждого Элемент Из СписокСертификатов Цикл
			Сертификат = Элемент.Значение;
			Если Сертификат.ИНН = ИНН.Значение Тогда
				Если Сертификат.Ключ.Тип = ""Серверный"" и Сертификат.Ключ.Активирован = ""Нет"" Тогда
					СписокСертификатовДляАктивации.Добавить(Сертификат);
				КонецЕсли;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;	
	
	Возврат СписокСертификатовДляАктивации;
КонецФункции
&НаКлиенте
Функция ПолучитьКодАктивацииСертификата(Кэш, Сертификат) Экспорт
	certificate = Кэш.Docflow.CreateSimpleObject();
	certificate.Write(""Отпечаток"", Сертификат.Отпечаток);
	Object = Кэш.docflow.AcquireCertificateActivation(certificate);
КонецФункции
&НаКлиенте
функция АктивироватьСерверныеСертификаты(Кэш, СписокСертификатов) Экспорт
	// функция активирует серверные сертификаты для определенного списка ИНН	
	Для Каждого Элемент Из СписокСертификатов Цикл
		Сертификат = Элемент.Значение;
		certificate = Кэш.Docflow.CreateSimpleObject();
		certificate.Write(""Отпечаток"", Сертификат.Отпечаток);
		certificate.Write(""КодАктивации"", Сертификат.КодАктивации);
		Object = Кэш.docflow.ActivateCertificate(certificate);
		Если Object = 0 Тогда //почему неопределено
			сбисСообщитьОбОшибке();
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат Истина;
КонецФункции
&НаКлиенте
функция сбисЗаписатьСотрудников(Кэш, ДанныеСотрудников, Отказ) Экспорт
	// функция активирует серверные сертификаты для определенного списка ИНН	
	СбисОшибка = Кэш.ОбщиеФункции.СбисИсключение(,""SDK2.сбисЗаписатьСотрудников"",,""Загрузка сотрудников в СБИС дступна только при способе обмена ExtSDK, ExtSDK2 или API."",""Изменить способ обмена можно на вкладке """"Настройки""""."");
	ДанныеПоСтатусу = Новый Структура();
	Кэш.Вставить(""РезультатОтправки"", Новый Структура(""ТипыОшибок,Отправлено,НеОтправлено,НеСформировано,Ошибок,ДетализацияОшибок,ВсегоПакетов, Успешные"", Новый СписокЗначений,0,0,0,0, Новый Соответствие,ДанныеСотрудников.Количество(), Новый Массив));
	Для Каждого Элемент Из ДанныеСотрудников Цикл
		Кэш.ОбщиеФункции.ДобавитьОшибкуВРезультат(Кэш, СбисОшибка, Новый Структура(""ОсновнойДокумент1С"", Элемент.Ключ));
	КонецЦикла;	
	Возврат Кэш.РезультатОтправки;
КонецФункции

//Метод устарел, вместо него использовать Кэш.СБИС.МодульОбъектаКлиент.СбисПолучитьИнформациюОТекущемПользователе
&НаКлиенте
Функция ИнформацияОТекущемПользователе(Кэш) Экспорт
	// Получает информацию о текущем пользователе	
	Результат = Кэш.Docflow.ReadCurrentUserInfo();	
	Если Результат = Неопределено Тогда //почему неопределено
		Возврат """";
	КонецЕсли;
	Результат = СериализоватьObjectВСтруктуру(Результат);
	Фамилия = """";
	Имя = """";
	Отчество = """";
	Результат.Пользователь.Свойство(""Фамилия"", Фамилия);
	Результат.Пользователь.Свойство(""Имя"", Имя);
	Результат.Пользователь.Свойство(""Отчество"", Отчество);
	Возврат Фамилия+"" ""+Имя+"" ""+Отчество;
КонецФункции
&НаКлиенте
Функция ПолучитьСообщениеОбОшибке(Кратко = Истина) Экспорт
	// Получает последнюю ошибку SDK	
	Ошибка = Новый Структура();
	Error = МестныйКэш.Docflow.ReadLastErrorEx();
	Попытка
		Ошибка.Вставить(""ТекстОшибки"", строка(Error.Read(""Описание"")));
		Ошибка.Вставить(""ИнформацияОбОшибке"", строка(Error.Read(""ОписаниеРасширенное"")));
	Исключение
		Ошибка.Вставить(""ТекстОшибки"", """");
		Ошибка.Вставить(""ИнформацияОбОшибке"", """");
	КонецПопытки;
	Ошибка.Вставить(""ПолныйТекст"",Ошибка.ТекстОшибки+?(Ошибка.ТекстОшибки<>Ошибка.ИнформацияОбОшибке,"" (""+Ошибка.ИнформацияОбОшибке+"")"","""" ));
	Если(Кратко)Тогда
		Возврат Ошибка.ТекстОшибки;
	КонецЕсли;
	Возврат Ошибка;
КонецФункции
&НаКлиенте
Функция сбисСессияДействительна(Кэш) Экспорт
	Результат=Кэш.Docflow.CheckSession();
	Возврат Результат;
КонецФункции
&НаКлиенте
функция ПрочитатьДокумент(Кэш,ИдДок,ДопПараметры=Неопределено,Отказ=Ложь) экспорт
	// Получает структуру документа СБИС	
	МестныйКэш = Кэш;
	document = Кэш.Docflow.CreateSimpleObject(); 
	document.Write( ""Идентификатор"", ИдДок ); 
	
	Если Не	ДопПараметры = Неопределено
		И	ДопПараметры.Свойство(""Этап"") Тогда
		document.WriteObject(""Этап"", СериализоватьСтруктуруВObject(ДопПараметры.Этап, Кэш));
	КонецЕсли;
	
	РезультатЧтения = Кэш.docflow.ReadDocument(document);
	Если РезультатЧтения = Неопределено Тогда//Метод не отработал, смотрим ошибку
		Отказ = Истина;
		Если ДопПараметры = Неопределено Тогда//Старый вызов, не ожидаем возврат ошибки
			сбисСообщитьОбОшибке();
			Возврат Ложь;
		Иначе
			checkparam = Неопределено;
			Ошибка = сбисПолучитьСтруктуруОшибки(Кэш);
			Если Не ДопПараметры.Свойство(""СообщатьПриОшибке"", checkparam)
				Или	checkparam Тогда
				Кэш.ГлавноеОкно.сбисСообщитьОбОшибке(Кэш, Ошибка);
			КонецЕсли;
			Если 	ДопПараметры.Свойство(""ВернутьОшибку"", checkparam)
				И	checkparam Тогда
				Возврат Ошибка; 
			КонецЕсли;
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	Возврат СериализоватьObjectВСтруктуру(РезультатЧтения);
КонецФункции
&НаКлиенте
функция ПолучитьДанныеФайла(Кэш,Ссылка) экспорт
	// Получает данные файла вложения	
	ИмяФайла = ПолучитьИмяВременногоФайла();
	Результат = СохранитьВложениеПоСсылкеВФайл(Кэш, Ссылка, ИмяФайла);
	Если Результат = Ложь Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстДок = Новый ЧтениеТекста(ИмяФайла);
	СтрокаТекст = СтрЗаменить(НРег(ТекстДок.ПрочитатьСтроку()),""'"","""""""");
	СтрокаКодировка = Кэш.КэшЗначенийИни.КодировкиЧтенияФайлов.ПоУмолчанию.ДляВсех;
	//Проверим кодировки для принудительного чтения.	
	Для Каждого КодировкаДляЧтения Из Кэш.КэшЗначенийИни.КодировкиЧтенияФайлов.Определять Цикл
		ПозицияКодировки = Найти(СтрокаТекст, ""encoding"");
		Если ПозицияКодировки И Найти(Сред(СтрокаТекст, ПозицияКодировки), КодировкаДляЧтения) Тогда
			СтрокаКодировка = КодировкаДляЧтения;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	//Переоткрываем файл в найденной кодировке
	ТекстДок.Открыть(ИмяФайла,СтрокаКодировка);
	РезультатТекст = ТекстДок.Прочитать();
	Попытка
		УдалитьФайлы(ИмяФайла);
	Исключение
	КонецПопытки;
	
	Попытка
		Если Лев(РезультатТекст,4)=""%PDF"" Тогда // для PDF-файла иногда возвращается крокозябра, которая потом вызывает ошибку передачи данных между клиентом и сервером, несмотря на то, что это строка
			Возврат """";
		КонецЕсли;
	Исключение
	КонецПопытки;
	Возврат РезультатТекст;
КонецФункции
&НаКлиенте
функция ПолучитьДанныеЗашифрованногоФайла(Кэш,Ссылка) экспорт
	// Получает данные файла вложения	
	Сообщить(""Для работы с зашифрованными документами выберите способ обмена """"extSDK2"""" на вкладке Настройки."");
	Возврат """";
КонецФункции
&НаКлиенте
функция ПроверитьПодписиВложения(Кэш,Вложение) экспорт
	// Расшифровывает данные файла вложения	
	Возврат Истина;
КонецФункции
&НаКлиенте
функция СохранитьВложениеПоСсылкеВФайл(Кэш,Ссылка,ИмяФайла) экспорт //d.ch
	Результат = Кэш.docflow.LoadDataFromURItoFile(Ссылка,ИмяФайла);
	Если Результат = 0 Тогда//получили ошибку при записи. Метод возвращает число.
		сбисСообщитьОбОшибке();
		Возврат Ложь;
	КонецЕсли;
	Возврат Результат;
КонецФункции
//&НаСервереБезКонтекста
//Функция ИмяРегистраСвойствОбъектов()
//	Если Метаданные.РегистрыСведений.Найти(""ЗначенияСвойствОбъектов"")<>Неопределено Тогда
//		Возврат ""ЗначенияСвойствОбъектов"";
//	ИначеЕсли Метаданные.РегистрыСведений.Найти(""ДополнительныеСведения"")<>Неопределено Тогда
//		Возврат ""ДополнительныеСведения"";
//	Иначе
//		// ??? где храним статусы
//	КонецЕсли
//КонецФункции
&НаКлиенте
функция ПолучитьHTMLВложения(Кэш,ИдДок, Вложение) экспорт
	// Получает html по идентификаторам пакета и вложения
	// Используется при просмотре документов из реестров СБИС
	Если Вложение.Свойство(""Зашифрован"") и Вложение.Зашифрован = ""Да"" тогда
		Возврат ""<HTML><BODY scroll=no>Документ зашифрован.</br>Для работы с зашифрованными документами выберите способ обмена """"extSDK2"""" на вкладке Настройки.</BODY></HTML>"";
	КонецЕсли;
	ext_document = Кэш.Docflow.CreateSimpleObject(); 
	ext_document.Write( ""Идентификатор"", Вложение.Идентификатор ); 
	ext_document.Write( ""СсылкаНаHTML"", Вложение.СсылкаНаHTML );
	document = Кэш.Docflow.CreateSimpleObject(); 
	document.Write( ""Идентификатор"", ИдДок ); 
	document.WriteObject( ""Вложение"", ext_document ); 
	html = Кэш.docflow.ReadDocumentAsHTML( document );
	Если html = Неопределено Тогда //почему неопределено
		Возврат """";
	КонецЕсли;
	
	html = СериализоватьObjectВСтруктуру(html);
	html_text = html.HTML;
	Возврат html_text;
КонецФункции
// << alo Меркурий
&НаКлиенте
функция СБИСЗаписатьВложения(Кэш,СоставПакета, Вложение) экспорт
	если СоставПакета.свойство(""Идентификатор"") и значениеЗаполнено(СоставПакета.Идентификатор) тогда
		StreamHelper = Новый COMОбъект(""SBIS.StreamHelper"");
		Если Вложение.Свойство(""ПолноеИмяФайла"") Тогда // внешний файл добавлен в пакет
			Имя= Вложение.ИмяФайла;
			ДвоичныеДанные= StreamHelper.FileToBase64(Вложение.ПолноеИмяФайла); 
		Иначе  // сформирован xml
			если Вложение.свойство(""СтруктураФайла"") тогда
				Имя= Вложение.СтруктураФайла.Файл.Имя+"".xml"";
			иначе
				Имя= Вложение.Файл.Имя+"".xml"";
			конецесли;
			если Вложение.свойство(""XMLДокумента"") тогда
				ДвоичныеДанные= StreamHelper.StringToBase64(Вложение.XMLДокумента);
			иначе
				ДвоичныеДанные="""";
			конецесли
		КонецЕсли;
		если ЗначениеЗаполнено(ДвоичныеДанные) тогда
			document = Кэш.Docflow.CreateSimpleObject();
			document.Write( ""Идентификатор"", СоставПакета.Идентификатор ); 
			attachmentList = Кэш.Docflow.CreateSimpleObjectList();
			attachment = Кэш.Docflow.CreateSimpleObject();
			file = Кэш.Docflow.CreateSimpleObject();
			file.Write( ""Имя"", Имя);
			file.Write( ""ДвоичныеДанные"", ДвоичныеДанные);
			attachment.WriteObject( ""Файл"", file );
			если не( Вложение.свойство(""Идентификатор"") и ЗначениеЗаполнено(Вложение.Идентификатор)) тогда
				ИдВложения = строка(Новый УникальныйИдентификатор());
				Вложение.Вставить(""Идентификатор"", ИдВложения);
			конецесли;
			attachment.Write( ""Идентификатор"",  Вложение.Идентификатор);
			attachmentList.Add( attachment );
			document.WriteObjectList( ""Вложение"", attachmentList );
			
			Результат = Кэш.docflow.WriteAttachment( document ); 
			Если Результат = Неопределено Тогда //почему неопределено
				//сбисСпрятатьСостояние(ГлавноеОкно);
				сбисСообщитьОбОшибке();
				Возврат Ложь;
			КонецЕсли;
			Возврат истина;
		иначе
			Сообщить(""Нет файла для записи"");
			Возврат Ложь;
		конецесли;
	иначе
		Сообщить(""Не указан документ для записи"");
		Возврат Ложь;
	конецесли;
КонецФункции		// alo Меркурий >>
&НаКлиенте
функция ПолучитьHTMLПоXML(Кэш, Вложение) экспорт
	// Получает html по xml	
	// Используется при просмотре документов из реестров продаж (1С)
	МестныйКэш = Кэш;
	param = Кэш.Docflow.CreateSimpleObject(); 
	Если ТипЗнч(Вложение) = Тип(""Структура"") Тогда
		param.Write( ""XML"", Вложение.XMLДокумента); 
	Иначе   // для совместимости с внешними функциями (СформироватьРасхождение)
		param.Write( ""XML"", Вложение);
	КонецЕсли;
	html = Кэш.docflow.GenerateHTMLFromXML( param );
	Если html=Неопределено Тогда
		сбисСообщитьОбОшибке();
		Возврат Ложь;
	КонецЕсли;
	html = СериализоватьObjectВСтруктуру(html);
	html_text = html.HTML;
	Возврат html_text;
КонецФункции
&НаКлиенте
Функция сбисВыполнитьКоманду(Кэш, Идентификатор,ИмяКоманды, ПредставлениеПакета) Экспорт
	// Выполняет указанную команду по документу СБИС (утверждение/отклонение)	
	МестныйКэш = Кэш;
	ГлавноеОкно = Кэш.ГлавноеОкно;
	
	document_out = МестныйКэш.Docflow.CreateSimpleObject();
	document_out.Write( ""Идентификатор"", Идентификатор );
	
	// Прочитаем пакет   
	СоставПакета = ПрочитатьДокумент(Кэш,Идентификатор);
	Если СоставПакета = Ложь Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если СоставПакета.Свойство(""Этап"") и (СоставПакета.Этап[0].Название  = ""Утверждение"" или СоставПакета.Этап[0].Название  = ""Утвердить"") Тогда
		action = Неопределено;
		Комментарий="""";
		Если ИмяКоманды = ""Отклонить"" Тогда
			Если НЕ ВвестиСтроку(Комментарий,""Причина отклонения"",,Истина) Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;	
		
		//Ищем действие соответсвующее команде
		Для Каждого Действие из СоставПакета.Этап[0].Действие Цикл
			Если Действие.Название = ИмяКоманды Тогда
				Возврат сбисВыполнитьДействие(Кэш, СоставПакета, СоставПакета.Этап[0], Действие, Комментарий, ПредставлениеПакета);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;	
	Возврат Ложь;
КонецФункции
&НаКлиенте
Функция сбисВыполнитьКомандуОтклонить(Комментарий, ДополнительныеПараметры) Экспорт
	// Выполняет указанную команду по документу СБИС (утверждение/отклонение)	
	
	Кэш = ДополнительныеПараметры.Кэш;
	Если НЕ ЗначениеЗаполнено(Комментарий) Тогда
		Кэш.ГлавноеОкно.сбисСообщитьОбОшибке(Кэш, Новый Структура(""code, message, details"", 780, ""Не заполнено обязательное поле"", ""Для выполнения операции, необходимо указать комментарий!""), Новый Структура());
		Возврат Ложь;
	КонецЕсли;
	
	СоставПакета = ДополнительныеПараметры.СоставПакета;
	ПредставлениеПакета = ДополнительныеПараметры.ПредставлениеПакета;
	ИмяКоманды = ДополнительныеПараметры.ИмяКоманды;
	ГлавноеОкно = ДополнительныеПараметры.ГлавноеОкно;
	
	//Ищем действие соответсвующее команде
	Для Каждого Действие из СоставПакета.Этап[0].Действие Цикл
		Если Действие.Название = ИмяКоманды Тогда
			ДействиеВыполнено = сбисВыполнитьДействие(Кэш, СоставПакета, СоставПакета.Этап[0], Действие, Комментарий, ПредставлениеПакета);
		КонецЕсли;
	КонецЦикла;
	
	ГлавноеОкно.ОбновитьКонтент();
КонецФункции
//Заглушка, подписание в SDK
&НаКлиенте
Функция сбисПодписатьВложения(Кэш, attachmentList, СертификатДляПодписания, Алгоритм, Отказ) Экспорт 
	Возврат Ложь;
КонецФункции
// Переводит документ повторно на ранее выполненный этап
&НаКлиенте
Функция сбисПовторитьЭтап(Кэш, ИдДок, ЭтапНазвание, Отказ=Ложь) Экспорт
	Отказ				= Истина;
	Сообщить(""Функционал доступен только для способов обмена """"extSDK"""" и """"API"""". Способ обмена можно изменить на вкладке """"Настройки""""."");
	Возврат Ложь;
КонецФункции
&НаКлиенте
Функция ОбработатьСлужебныеДокументыПоПакету(Кэш, СоставПакета) Экспорт
	// Обрабатывает служебные по одному пакету
	МестныйКэш = Кэш;
	param   = МестныйКэш.Docflow.CreateSimpleObject();
	param.Write(""ИдентификаторДокумента"",СоставПакета.Идентификатор);
	ИнформацияПоНеобработанным = МестныйКэш.Docflow.ReadServiceStagesInfo(param);
	Если ИнформацияПоНеобработанным = Неопределено Тогда //почему неопределено
		сбисСообщитьОбОшибке();
	КонецЕсли;
	ИнформацияПоНеобработанным = СериализоватьObjectВСтруктуру(ИнформацияПоНеобработанным);
	Всего = Число(ИнформацияПоНеобработанным.ЧислоНеобработанныхЭтапов);
	
	Если Всего>0 Тогда
		Результат = МестныйКэш.Docflow.ProcessServiceStagesEx(param);
		Если Результат = Неопределено Тогда //почему неопределено
			сбисСообщитьОбОшибке();
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
КонецФункции

// Получает Информацию о контрагенте с онлайна
&НаКлиенте
Функция ПолучитьИнформациюОКонтрагенте(Кэш, СтруктураКонтрагента) Экспорт
	Отказ = Ложь;
	kontr = Новый Структура; 
	Если СтруктураКонтрагента.Свойство(""СвФЛ"") Тогда
		СвФЛ = Новый Структура;
		СвФЛ.Вставить( ""ИНН"", СтруктураКонтрагента.СвФЛ.ИНН ); 
		kontr.Вставить( ""СвФЛ"", СвФЛ );	
	Иначе
		СвЮЛ = Новый Структура;
		СвЮЛ.Вставить( ""ИНН"", СтруктураКонтрагента.СвЮЛ.ИНН ); 
		СвЮЛ.Вставить( ""КПП"", СтруктураКонтрагента.СвЮЛ.КПП );
		Если СтруктураКонтрагента.СвЮЛ.Свойство(""КодФилиала"") Тогда
			СвЮЛ.Вставить( ""КодФилиала"", СтруктураКонтрагента.СвЮЛ.КодФилиала );
		КонецЕсли;
		kontr.Вставить( ""СвЮЛ"", СвЮЛ );
	КонецЕсли;
	
	СтруктураРезультата = СБИС_ИнформацияОКонтрагенте(Кэш, kontr, Новый Структура, Отказ);
	Если Отказ Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат СтруктураРезультата;
КонецФункции
&НаКлиенте
Функция ОбработкаСлужебныхДокументов(Кэш) Экспорт
	// Получает список организаций с необработанными этапами и запускает для них обработку служебных документов
	МестныйКэш = Кэш;
	ГлавноеОкно = Кэш.ГлавноеОкно;
	сбисПоказатьСостояние(""Обработка служебных документов"",ГлавноеОкно);
	СписокИНН = Новый СписокЗначений;
	МассивОрганизаций = ПолучитьСписокНашихОрганизаций(Кэш, СписокИНН);
	СписокСертификатов = Кэш.Интеграция.ПолучитьСертификатыДляАктивации(Кэш, СписокИНН);
	Если СписокСертификатов.Количество()>0 Тогда
		Для Каждого Элемент Из СписокСертификатов Цикл
			Сертификат = Элемент.Значение;
			Если Сертификат.Ключ.СпособАктивации<>""СтатическийКод"" Тогда
				ПолучитьКодАктивацииСертификата(Кэш, Сертификат);				
			КонецЕсли;
			ФормаВводаПинкода = ГлавноеОкно.сбисПолучитьФорму(""ФормаВводаПинкода"");
			ПараметрыВвода = Новый Структура(""СертификатИмя"", Кэш.ОбщиеФункции.СформироватьЗаголовокСертификатаДляФормыВвода(Кэш, Сертификат));
			КодАктивации = ФормаВводаПинкода.Показать(ПараметрыВвода);
			Если	Не	ЗначениеЗаполнено(КодАктивации)
				Или		КодАктивации.ПинКод = """" Тогда
				Сообщить(""Не активирован сертификат для подписания документов."");
				Возврат Ложь;	
			КонецЕсли;
			Сертификат.Вставить(""КодАктивации"", КодАктивации.ПинКод);
		КонецЦикла;
		Если Кэш.Интеграция.АктивироватьСерверныеСертификаты(Кэш, СписокСертификатов) = Ложь Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;	
	Для Каждого оОрганизация из МассивОрганизаций Цикл
		ИзвещенияОбработаны = ОбработатьСлужебныеДокументыОрганизации(Кэш, оОрганизация);
	КонецЦикла;	
	сбисСпрятатьСостояние(ГлавноеОкно);
КонецФункции
&НаКлиенте
Функция ОбработатьСлужебныеДокументыОрганизации(Кэш, оОрганизация)
	// Обрабатывает служебные документы организации	
	ГлавноеОкно = Кэш.ГлавноеОкно;
	param   = МестныйКэш.Docflow.CreateSimpleObject();
	Орг = СериализоватьObjectВСтруктуру(оОрганизация.com);
	org = МестныйКэш.Docflow.CreateSimpleObject(); 
	Если Орг.Свойство(""СвФЛ"") Тогда
		СвФЛ = МестныйКэш.Docflow.CreateSimpleObject();
		СвФЛ.Write( ""ИНН"", Орг.СвФЛ.ИНН); 
		org.WriteObject( ""СвФЛ"", СвФЛ );	
	Иначе
		СвЮЛ = МестныйКэш.Docflow.CreateSimpleObject();
		СвЮЛ.Write( ""ИНН"", Орг.СвЮЛ.ИНН ); 
		СвЮЛ.Write( ""КПП"", Орг.СвЮЛ.КПП );
		Если Орг.СвЮЛ.Свойство(""КодФилиала"") и ЗначениеЗаполнено(Орг.СвЮЛ.КодФилиала) Тогда
			СвЮЛ.Write( ""КодФилиала"", Орг.СвЮЛ.КодФилиала );	
		КонецЕсли;
		org.WriteObject( ""СвЮЛ"", СвЮЛ );
	КонецЕсли;
	param.WriteObject(""НашаОрганизация"",org);
	сбисПоказатьСостояние(""Обработка служебных документов ""+оОрганизация.Название,ГлавноеОкно);
	ИнформацияПоНеобработанным = МестныйКэш.Docflow.ReadServiceStagesInfo(param);
	Если ИнформацияПоНеобработанным = Неопределено Тогда //почему неопределено
		сбисСообщитьОбОшибке();
		Возврат Ложь;
	КонецЕсли;
	ИнформацияПоНеобработанным = СериализоватьObjectВСтруктуру(ИнформацияПоНеобработанным);
	Всего = Число(ИнформацияПоНеобработанным.ЧислоНеобработанныхЭтапов);
	
	Если Всего>0 Тогда
		ЕстьНеобработанныеИзвещения = ""Да"";
		Обработано = 0;
		ПодключитьОбработчикОжидания(""сбисПрерываниеПользователем"",0.1,Истина);
		Пока ЕстьНеобработанныеИзвещения = ""Да"" Цикл
			Результат = МестныйКэш.Docflow.ProcessServiceStagesEx(param);
			Если Результат = Неопределено Тогда //почему неопределено
				сбисСпрятатьСостояние(ГлавноеОкно);
				сбисСообщитьОбОшибке();
				Возврат Ложь;
			КонецЕсли;
			Результат = СериализоватьObjectВСтруктуру(Результат);
			ЕстьНеобработанныеИзвещения = Результат.ЕстьНеобработанныеЭтапы;
			Обработано = Обработано + Число(Результат.ОбработаноЭтапов);
			
			сбисПоказатьСостояние(""Обработка служебных документов ""+оОрганизация.Название,ГлавноеОкно,Мин(100,Окр(Обработано*100/Всего)), ""(прервать -  Ctrl+Break)"");
			
			Если Обработано=0 и ЕстьНеобработанныеИзвещения = ""Да"" Тогда     // Необработанные есть, но обработать по какой-то причине не можем
				Прервать;
			КонецЕсли;
			ОбработкаПрерыванияПользователя();
		КонецЦикла;
		ОтключитьОбработчикОжидания(""сбисПрерываниеПользователем"");
		сбисСпрятатьСостояние(ГлавноеОкно);
	КонецЕсли;
	Возврат Истина;
КонецФункции
&НаКлиенте
Функция ОтправитьПакетыДокументов(Кэш, МассивПакетов, РезультатОтправки=Неопределено) Экспорт
	Перем ДопПараметрыСтороны;
	// Отправляет сформированные пакеты документов	
	КоличествоПакетов = МассивПакетов.Количество();
	Если КоличествоПакетов = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	ГлавноеОкно = Кэш.ГлавноеОкно;
	МассивСтатусРегламент = Новый Массив;
	
	фрм = ГлавноеОкно.сбисНайтиФормуФункции(""ЗаписатьПараметрыДокументовСБИС"",Кэш.ФормаРаботыСоСтатусами,"""",Кэш);
	// << alo 
	Если Кэш.парам.СостояниеЭД Тогда
		ПараметрыПоиска = Новый Структура;
		ПараметрыПоиска.Вставить(""ИмяФункции"",	""ЗаписатьПараметрыДокументовСБИС"");
		ПараметрыПоиска.Вставить(""КлючФорм"",	""Статусы_СостоянияЭД"");
		фрмЭД = Кэш.ОбщиеФункции.сбисНайтиФормуФункцииПодсистемы(Кэш, ПараметрыПоиска);
	КонецЕсли;
	// alo >>
	сбисПоказатьСостояние(""Отправка документов"", ГлавноеОкно,, ""(прервать -  Ctrl+Break)"");
	ВсегоОтправлено = 0;
	ВсегоОшибок = 0;
	StreamHelper = Новый COMОбъект(""SBIS.StreamHelper"");
	documenty = Кэш.Docflow.CreateSimpleObjectList();
	
	//KES 050751151 Статусы в разрезе регламентов (ОТПРАВКА ДОКУМЕНТОВ)--> 42
	Если  Кэш.ФормаРаботыСоСтатусами = ""Статусы_Регистры"" 
		И Кэш.ини.Конфигурация.Свойство(""СтатусРегламент"") Тогда
			МассивСтатусРегламент =  Кэш.ОбщиеФункции.РазбитьСтрокуВМассивНаКлиенте(Кэш.ини.Конфигурация.СтатусРегламент.Значение,"","");
	КонецЕсли;
	//<-- KES 050751151 Статусы в разрезе регламентов (ОТПРАВКА ДОКУМЕНТОВ)

	Для СчетчикПакетов=0 По КоличествоПакетов-1 Цикл
		СоставПакета = МассивПакетов[СчетчикПакетов];
		document = Кэш.Docflow.CreateSimpleObject();
		Если СоставПакета.Свойство(""Дата"") и ЗначениеЗаполнено(СоставПакета.Дата) Тогда
			document.Write( ""Дата"",СоставПакета.Дата);	
		КонецЕсли;
		Если СоставПакета.Свойство(""Номер"") и ЗначениеЗаполнено(СоставПакета.Номер) Тогда
			document.Write( ""Номер"",СоставПакета.Номер);	
		КонецЕсли;
		Если СоставПакета.Свойство(""Сумма"") и ЗначениеЗаполнено(СоставПакета.Сумма) Тогда
			document.Write( ""Сумма"",СоставПакета.Сумма);	
		КонецЕсли;
		attachmentList = Кэш.Docflow.CreateSimpleObjectList();
		Для Каждого Вложение Из СоставПакета.Вложение Цикл
			attachment = Кэш.Docflow.CreateSimpleObject();
			Если Вложение.Свойство(""Зашифрован"") Тогда   // может быть в случае пересылки зашифрованных пакетов
				attachment.Write( ""Зашифрован"", Вложение.Зашифрован);
			КонецЕсли;
			file = Кэш.Docflow.CreateSimpleObject();
			Если Вложение.Свойство(""ПолноеИмяФайла"") Тогда // внешний файл добавлен в пакет
				file.Write( ""Имя"", Вложение.ИмяФайла ); 
				file.Write( ""ДвоичныеДанные"", StreamHelper.FileToBase64(Вложение.ПолноеИмяФайла) ); 
			Иначе  // сформирован xml
				ИмяФайла = Вложение.СтруктураФайла.Файл.Имя+"".xml"";
				file.Write( ""Имя"", ИмяФайла ); 
				file.Write( ""ДвоичныеДанные"", StreamHelper.StringToBase64(Вложение.XMLДокумента) ); 
			КонецЕсли;
			
			Если Вложение.Свойство(""Подпись"") Тогда //d.ch
				ЭЦП = Кэш.Docflow.CreateSimpleObjectList();
				Для Каждого Запись из Вложение.Подпись Цикл
					ЗаписьЭЦП = Кэш.Docflow.CreateSimpleObject();
					Если Запись.Свойство(""Направление"") Тогда
						ЗаписьЭЦП.Write(""Направление"",Запись.Направление);
					КонецЕсли;
					ФайлЭЦП = Кэш.Docflow.CreateSimpleObject();
					ФайлЭЦП.Write( ""Имя"", Запись.Файл.Имя ); 
					ФайлЭЦП.Write( ""ДвоичныеДанные"",StreamHelper.FileToBase64(Запись.Файл.ПолноеИмяФайла)); 
					ЗаписьЭЦП.WriteObject(""Файл"",ФайлЭЦП);
					ЭЦП.Add(ЗаписьЭЦП);
				КонецЦикла;
				attachment.WriteObjectList( ""Подпись"", ЭЦП );
			КонецЕсли;
			
			attachment.WriteObject( ""Файл"", file );
			ИдВложения = строка(Новый УникальныйИдентификатор());
			Вложение.Вставить(""Идентификатор"", ИдВложения);
			attachment.Write( ""Идентификатор"",  ИдВложения);
			// Пока не указываем, т.к. онлайн в этом случае не формирует красивые названия вложений
			//Если Вложение.Свойство(""Тип"") и ЗначениеЗаполнено(Вложение.Тип) и Вложение.Свойство(""ПодТип"") и ЗначениеЗаполнено(Вложение.ПодТип) и Вложение.Свойство(""ВерсияФормата"") и ЗначениеЗаполнено(Вложение.ВерсияФормата) Тогда
			//	attachment.Write( ""Тип"",  Вложение.Тип);
			//	attachment.Write( ""Подтип"",  Вложение.ПодТип);
			//	attachment.Write( ""ВерсияФормата"",  Вложение.ВерсияФормата);
			//	Если Вложение.Свойство(""ПодВерсияФормата"") и ЗначениеЗаполнено(Вложение.ПодВерсияФормата) Тогда
			//		attachment.Write( ""ПодверсияФормата"",  Вложение.ПодВерсияФормата);
			//	КонецЕсли;
			//КонецЕсли;
			attachmentList.Add( attachment );
		КонецЦикла;
		document.Write( ""Тип"",СоставПакета.Тип);	
		Если СоставПакета.Свойство(""Подтип"") и ЗначениеЗаполнено(СоставПакета.Подтип) Тогда
			document.Write( ""Подтип"",СоставПакета.Подтип);
		КонецЕсли;
		Если СоставПакета.Свойство(""ПользовательскийИдентификатор"") и Не (СоставПакета.Свойство(""Контрагент"") и СоставПакета.Контрагент.Свойство(""ЗапретРедакций"")) Тогда
			ИдПакета = Сред(СоставПакета.ПользовательскийИдентификатор,Найти(СоставПакета.ПользовательскийИдентификатор,"":"")+1);
		Иначе
			ИдПакета = строка(Новый УникальныйИдентификатор());
		КонецЕсли;
		
		СоставПакета.Вставить(""Идентификатор"", ИдПакета);
		document.Write( ""Идентификатор"", ИдПакета ); 
		Если СоставПакета.Свойство(""ПользовательскийИдентификатор"") Тогда
			redaction = Кэш.Docflow.CreateSimpleObject();
			redaction.Write(""ИдентификаторИС"", СоставПакета.ПользовательскийИдентификатор);
			document.WriteObject( ""Редакция"", redaction);
		КонецЕсли;
		Если СоставПакета.Свойство(""Примечание"") и ЗначениеЗаполнено(СоставПакета.Примечание) Тогда
			document.Write( ""Примечание"", СоставПакета.Примечание);
		КонецЕсли;
		document.WriteObjectList( ""Вложение"", attachmentList );
		
		org = Кэш.Docflow.CreateSimpleObject(); 
		Если СоставПакета.НашаОрганизация.Свойство(""СвФЛ"") Тогда
			СвФЛ = Кэш.Docflow.CreateSimpleObject();
			СвФЛ.Write( ""ИНН"", СоставПакета.НашаОрганизация.СвФЛ.ИНН); 
			Если СоставПакета.НашаОрганизация.СвФЛ.Свойство(""КодФилиала"") и ЗначениеЗаполнено(СоставПакета.НашаОрганизация.СвФЛ.КодФилиала) Тогда
				СвФЛ.Write( ""КодФилиала"", СоставПакета.НашаОрганизация.СвФЛ.КодФилиала );	
			КонецЕсли;
			org.WriteObject( ""СвФЛ"", СвФЛ );
		Иначе
			СвЮЛ = Кэш.Docflow.CreateSimpleObject();
			СвЮЛ.Write( ""ИНН"", СоставПакета.НашаОрганизация.СвЮЛ.ИНН ); 
			СвЮЛ.Write( ""КПП"", СоставПакета.НашаОрганизация.СвЮЛ.КПП );
			Если СоставПакета.НашаОрганизация.СвЮЛ.Свойство(""КодФилиала"") и ЗначениеЗаполнено(СоставПакета.НашаОрганизация.СвЮЛ.КодФилиала) Тогда
				СвЮЛ.Write( ""КодФилиала"", СоставПакета.НашаОрганизация.СвЮЛ.КодФилиала );	
			КонецЕсли;
			Если СоставПакета.НашаОрганизация.СвЮЛ.Свойство(""КодСтраны"") Тогда
				СвЮЛ.Write( ""КодСтраны"", СоставПакета.НашаОрганизация.СвЮЛ.КодСтраны );	
			КонецЕсли;
			org.WriteObject( ""СвЮЛ"", СвЮЛ );
		КонецЕсли;
		document.WriteObject( ""НашаОрганизация"", org );
		
		Если СоставПакета.Свойство(""Контрагент"") Тогда
			kontr = Кэш.Docflow.CreateSimpleObject(); 
			Если СоставПакета.Контрагент.Свойство(""СвФЛ"") Тогда
				СвФЛ = Кэш.Docflow.CreateSimpleObject();
				Если СоставПакета.Контрагент.Свойство(""Параметр"") Тогда
					Для Каждого Параметр из СоставПакета.Контрагент.Параметр Цикл
						Если Параметр.Имя = ""ЧастноеЛицо"" Тогда
							СвФЛ.Write( ""ЧастноеЛицо"", Параметр.Значение);
						КонецЕсли;
						Если Параметр.Имя = ""СНИЛС"" Тогда
							СвФЛ.Write(""СНИЛС"", Параметр.Значение);
						КонецЕсли;     					
					КонецЦикла;
				КонецЕсли;
				Если СоставПакета.Контрагент.СвФЛ.Свойство(""ИНН"") Тогда
					СвФЛ.Write( ""ИНН"", СоставПакета.Контрагент.СвФЛ.ИНН ); 
				КонецЕсли; 
				Если СоставПакета.Контрагент.СвФЛ.Свойство(""Фамилия"") Тогда
					СвФЛ.Write( ""Фамилия"", СоставПакета.Контрагент.СвФЛ.Фамилия );	
				КонецЕсли;
				Если СоставПакета.Контрагент.СвФЛ.Свойство(""Имя"") Тогда
					СвФЛ.Write( ""Имя"", СоставПакета.Контрагент.СвФЛ.Имя );	
				КонецЕсли;
				Если СоставПакета.Контрагент.СвФЛ.Свойство(""Отчество"") Тогда
					СвФЛ.Write( ""Отчество"", СоставПакета.Контрагент.СвФЛ.Отчество );	
				КонецЕсли;
				Если СоставПакета.Контрагент.СвФЛ.Свойство(""КодФилиала"") и ЗначениеЗаполнено(СоставПакета.Контрагент.СвФЛ.КодФилиала) Тогда
					СвФЛ.Write( ""КодФилиала"", СоставПакета.Контрагент.СвФЛ.КодФилиала );	
				КонецЕсли;
				kontr.WriteObject( ""СвФЛ"", СвФЛ );	
			Иначе
				СвЮЛ = Кэш.Docflow.CreateSimpleObject();
				СвЮЛ.Write( ""ИНН"", СоставПакета.Контрагент.СвЮЛ.ИНН ); 
				Если СоставПакета.Контрагент.СвЮЛ.Свойство(""КПП"") Тогда // << bav
					СвЮЛ.Write( ""КПП"", СоставПакета.Контрагент.СвЮЛ.КПП );
				КонецЕсли;
				Если СоставПакета.Контрагент.СвЮЛ.Свойство(""КодСтраны"") Тогда
					СвЮЛ.Write( ""КодСтраны"", СоставПакета.Контрагент.СвЮЛ.КодСтраны );	
				КонецЕсли; // bav >>
				Если СоставПакета.Контрагент.СвЮЛ.Свойство(""КодФилиала"") и ЗначениеЗаполнено(СоставПакета.Контрагент.СвЮЛ.КодФилиала) Тогда
					СвЮЛ.Write( ""КодФилиала"", СоставПакета.Контрагент.СвЮЛ.КодФилиала );	
				КонецЕсли;
				Если СоставПакета.Контрагент.СвЮЛ.Свойство(""Название"") Тогда
					СвЮЛ.Write( ""Название"", СоставПакета.Контрагент.СвЮЛ.Название );	
				КонецЕсли;
				kontr.WriteObject( ""СвЮЛ"", СвЮЛ );
			КонецЕсли;
			Если СоставПакета.Контрагент.Свойство(""Подразделение"") и СоставПакета.Контрагент.Подразделение.Свойство(""Идентификатор"") Тогда
				Подразделение = Кэш.Docflow.CreateSimpleObject();
				Подразделение.Write( ""Идентификатор"", СоставПакета.Контрагент.Подразделение.Идентификатор); 
				kontr.WriteObject( ""Подразделение"", Подразделение );
			КонецЕсли;
			Если СоставПакета.Контрагент.Свойство(""Контакт"")  Тогда
				Если СоставПакета.Контрагент.Контакт.Свойство(""Телефон"")  Тогда
					kontr.Write( ""Телефон"", СоставПакета.Контрагент.Контакт.Телефон );
				КонецЕсли;
				Если СоставПакета.Контрагент.Контакт.Свойство(""EMAIL"")  Тогда
					kontr.Write( ""Email"", СоставПакета.Контрагент.Контакт.EMAIL );
				КонецЕсли;
			КонецЕсли;
			document.WriteObject( ""Контрагент"", kontr );
		КонецЕсли;
		Если	СоставПакета.Контрагент.Свойство(""Идентификатор"")
			И	ЗначениеЗаполнено(СоставПакета.Контрагент.Идентификатор) Тогда
			kontr.Write( ""Идентификатор"", СоставПакета.Контрагент.Идентификатор);
		Иначе			КлючДляДопПараметров = Кэш.ОбщиеФункции.КлючСтороныДляДопПараметров(СоставПакета.Контрагент, Новый Структура);
			Если	Кэш.ТекущийСеанс.РасчитанныеЗначения.Свойство(""мСторона"")
				И	Кэш.ТекущийСеанс.РасчитанныеЗначения.мСторона.Свойство(КлючДляДопПараметров, ДопПараметрыСтороны)
				И	ДопПараметрыСтороны.Свойство(""КодОператораАбонентскогоЯщика"") Тогда
				//ИД явно не указан, то проверить наличие ИД оператора А/Я
				kontr.Write( ""Идентификатор"", ДопПараметрыСтороны.КодОператораАбонентскогоЯщика);
			КонецЕсли;
		КонецЕсли;
		Если СоставПакета.Свойство(""Ответственный"") и СоставПакета.Ответственный.Количество()>0 Тогда
			otv = Кэш.Docflow.CreateSimpleObject();
			Для Каждого Элемент Из СоставПакета.Ответственный Цикл
				otv.Write( Элемент.Ключ, Элемент.Значение );	
			КонецЦикла;
			document.WriteObject( ""Ответственный"", otv );	
		КонецЕсли;
		Если СоставПакета.Свойство(""Подразделение"") и СоставПакета.Подразделение.Количество()>0 Тогда
			podrazdel = Кэш.Docflow.CreateSimpleObject();
			Для Каждого Элемент Из СоставПакета.Подразделение Цикл
				podrazdel.Write( Элемент.Ключ, Элемент.Значение );	
			КонецЦикла;
			document.WriteObject( ""Подразделение"", podrazdel ); 	
		КонецЕсли;
		Если СоставПакета.Свойство(""Регламент"") и СоставПакета.Регламент.Количество()>0 Тогда
			regl = Кэш.Docflow.CreateSimpleObject();
			Для Каждого Элемент Из СоставПакета.Регламент Цикл
				regl.Write( Элемент.Ключ, Элемент.Значение );	
			КонецЦикла;
			document.WriteObject( ""Регламент"", regl ); 	
		КонецЕсли;
		Если СоставПакета.Свойство(""ДокументОснование"") и СоставПакета.ДокументОснование.Количество()>0 Тогда
			osnovania = Кэш.Docflow.CreateSimpleObjectList();
			Для Каждого ДокОсн Из СоставПакета.ДокументОснование Цикл 
				osn = Кэш.Docflow.CreateSimpleObject();
				Если ДокОсн.Свойство(""ВидСвязи"") Тогда
					osn.Write( ""ВидСвязи"", ДокОсн.ВидСвязи );	
				КонецЕсли;
				doc = Кэш.Docflow.CreateSimpleObject();
				Для Каждого Элемент Из ДокОсн Цикл
					Если Элемент.Ключ<>""ВидСвязи"" Тогда
						doc.Write( Элемент.Ключ, СокрЛП(Элемент.Значение ));
					КонецЕсли;
				КонецЦикла;
				osn.WriteObject( ""Документ"", doc );
				osnovania.Add(osn);
			КонецЦикла;
			document.WriteObjectList( ""ДокументОснование"", osnovania ); 				
		КонецЕсли;
		documenty.Add(document);
	КонецЦикла;	
	
	param = Кэш.Docflow.CreateSimpleObject();
	param.WriteObjectList( ""Документ"", documenty );
	Если МассивПакетов[0].Свойство(""НеЗапускатьВДокументооборот"") и МассивПакетов[0].НеЗапускатьВДокументооборот = Истина Тогда
		param.Write( ""НеЗапускатьВДокументооборот"", ""Да"" );
	КонецЕсли;
	Если Кэш.Ини.Конфигурация.Свойство(""ЧислоПотоковОтправки"") Тогда
		param.Write( ""ЧислоПотоков"", СтрЗаменить(Кэш.Ини.Конфигурация.ЧислоПотоковОтправки.Значение,""'"","""") );	
	Иначе
		param.Write( ""ЧислоПотоков"", ""10"" );
	КонецЕсли;
	ДатаНач = ТекущаяДата();
	Результат = Кэш.docflow.WriteDocumentsEx(param);	
	ДатаКнц = ТекущаяДата();
	Кэш.РезультатОтправки.ВремяОтправки = Кэш.РезультатОтправки.ВремяОтправки+(ДатаКнц-ДатаНач);
	Если Результат = Неопределено Тогда //почему неопределено
		сбисСпрятатьСостояние(ГлавноеОкно);
		ТекстОшибки = сбисСообщитьОбОшибке();
		ЭлементСписка = Кэш.РезультатОтправки.ТипыОшибок.НайтиПоЗначению(ТекстОшибки);
		Если ЭлементСписка=Неопределено Тогда
			Кэш.РезультатОтправки.ТипыОшибок.Добавить(ТекстОшибки, КоличествоПакетов-ВсегоОтправлено-ВсегоОшибок);
		Иначе
			ЭлементСписка.Представление = Число(ЭлементСписка.Представление)+КоличествоПакетов-ВсегоОтправлено-ВсегоОшибок;
		КонецЕсли;
		Если НЕ Кэш.РезультатОтправки.Свойство(""ПрерватьОтправку"") Тогда
			Кэш.РезультатОтправки.Вставить(""ПрерватьОтправку"", ТекстОшибки);
		КонецЕсли;
		сбисСпрятатьСостояние(ГлавноеОкно);
		Возврат Ложь;
	КонецЕсли;
	// Обрабатываем результат отправки - проставляем статусы
	Результат = СериализоватьObjectВСтруктуру(Результат);
	ДанныеПоСтатусам = Новый Массив;
	сч = 0;
	Для Каждого Элемент Из Результат.Реестр Цикл
		СоставПакета = МассивПакетов[сч];
		
		//KES 050751151 Статусы в разрезе регламентов (ОТПРАВКА ДОКУМЕНТОВ)--> 43
		СтатусРегламент = """";
		Если СоставПакета.Свойство(""Регламент"") 
			И СоставПакета.Регламент.Свойство(""Название"") 
			И НЕ МассивСтатусРегламент.Найти(СоставПакета.Регламент.Название)=Неопределено Тогда
				СтатусРегламент = СоставПакета.Регламент.Название; 
		КонецЕсли;
		//<-- KES 050751151 Статусы в разрезе регламентов (ОТПРАВКА ДОКУМЕНТОВ)

		Если Элемент.Свойство(""Документ"") Тогда
			СоставПакета.Вставить(""Отправлен"", Истина);
			ВсегоОтправлено = ВсегоОтправлено + 1;
			Кэш.РезультатОтправки.Отправлено = Кэш.РезультатОтправки.Отправлено + 1;
			ТекстСообщения = """";
			Для Каждого Вложение Из СоставПакета.Вложение Цикл
				Если Вложение.Свойство(""Документы1С"") Тогда
					Для Каждого Документ1С Из Вложение.Документы1С Цикл
						СтруктураСвойств = Новый Структура(""ДокументСБИС_Ид,ДокументСБИС_ИдВложения,ДокументСБИС_Статус"", СоставПакета.Идентификатор,Вложение.Идентификатор,Элемент.Документ.Состояние.Название);
						//KES 050751151 Статусы в разрезе регламентов (ОТПРАВКА ДОКУМЕНТОВ)--> 44
						ДанныеПоСтатусам.Добавить(Новый Структура(""СтруктураСвойств, Документ1С,Регламент"",СтруктураСвойств, Документ1С.Значение,СтатусРегламент));
						//<-- KES 050751151 Статусы в разрезе регламентов (ОТПРАВКА ДОКУМЕНТОВ) 
						ТекстСообщения = ТекстСообщения+"", ""+строка(Документ1С.Значение);	
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
			ТекстСообщения = ""Отправлен пакет документов: ""+Сред(ТекстСообщения, 3);
			//Сообщить(ТекстСообщения);	
		КонецЕсли;
		Если Элемент.Свойство(""Ошибка"") Тогда
			// если истекла сессия, то переавторизуемся и переотправим партию документов
			Если Элемент.Ошибка.Описание = ""Необходимо аутентифицироваться для выполнения запросов к серверу"" или Лев(Элемент.Ошибка.Описание, 18) = ""Ошибка авторизации"" Тогда
				Если (Кэш.Парам.ВходПоСертификату и Кэш.Парам.ЗапомнитьСертификат) или (НЕ Кэш.Парам.ВходПоСертификату и Кэш.Парам.ЗапомнитьПароль) Тогда
					формаАвторизации = ГлавноеОкно.сбисПолучитьФорму(""ФормаАвторизация"");
					УспешнаяАвторизация = формаАвторизации.Авторизоваться(Кэш);
					Если УспешнаяАвторизация = Истина Тогда
						Кэш.РезультатОтправки.Вставить(""ПереотправитьПартию"", Истина);
						Прервать; // ошибки не записываем, так как будем переотправлять неотправленные
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			ВсегоОшибок = ВсегоОшибок + 1;
			Кэш.РезультатОтправки.Ошибок = Кэш.РезультатОтправки.Ошибок + 1;
			ТекстОшибки = Элемент.Ошибка.Описание;
			ЭлементСписка = Кэш.РезультатОтправки.ТипыОшибок.НайтиПоЗначению(ТекстОшибки);
			Если ЭлементСписка=Неопределено Тогда
				Кэш.РезультатОтправки.ТипыОшибок.Добавить(ТекстОшибки, 1);
			Иначе
				ЭлементСписка.Представление = Число(ЭлементСписка.Представление)+1;
			КонецЕсли;
			Если СоставПакета.Вложение.Количество()>0 и СоставПакета.Вложение[0].Свойство(""Документы1С"") Тогда
				ОсновнойДокумент1С = СоставПакета.Вложение[0].Документы1С[0].Значение;
			Иначе
				ОсновнойДокумент1С = Неопределено;
			КонецЕсли;
			//AU изменена структура в детализации ошибок для возможности проброса дампа в сервис статистики
			ЭлементСоответствия = Кэш.РезультатОтправки.ДетализацияОшибок.Получить(ТекстОшибки);
			Если ЭлементСоответствия=Неопределено Тогда
				ЭлементСоответствия = Новый Массив;
				Кэш.РезультатОтправки.ДетализацияОшибок.Вставить(ТекстОшибки, ЭлементСоответствия);
			КонецЕсли;
			СтрокаВСоответствие = Новый Структура(""ОбработанДокумент1С,Сообщение,СтруктураОшибки"", ОсновнойДокумент1С, Элемент.Ошибка.ОписаниеРасширенное, Новый Структура(""message,details,code"", ТекстОшибки, Элемент.Ошибка.ОписаниеРасширенное, 100));
			ЭлементСоответствия.Добавить(СтрокаВСоответствие);
			ТекстСообщения = """";
			Для Каждого Вложение Из СоставПакета.Вложение Цикл
				Если Вложение.Свойство(""Документы1С"") Тогда
					Для Каждого Документ1С Из Вложение.Документы1С Цикл
						СтруктураСвойств = Новый Структура(""ДокументСБИС_Ид,ДокументСБИС_ИдВложения,ДокументСБИС_Статус"", СоставПакета.Идентификатор,Вложение.Идентификатор,""Ошибка: ""+Лев(ТекстОшибки, 230));
						
						//KES 050751151 Статусы в разрезе регламентов (ОТПРАВКА ДОКУМЕНТОВ)--> 45
						ДанныеПоСтатусамСтруктура = Новый Структура(""СтруктураСвойств, Документ1С, Регламент"",СтруктураСвойств, Документ1С.Значение,СтатусРегламент);
						//<-- KES 050751151 Статусы в разрезе регламентов (ОТПРАВКА ДОКУМЕНТОВ)

						Кэш.РезультатОтправки.ДанныеПоСтатусам.Добавить(ДанныеПоСтатусамСтруктура);
						ДанныеПоСтатусам.Добавить(ДанныеПоСтатусамСтруктура);
						ТекстСообщения = ТекстСообщения+"", ""+строка(Документ1С.Значение);	
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
			ТекстСообщения = ""Пакет документов не отправлен: ""+Сред(ТекстСообщения, 3)+"". ""+ТекстОшибки;
			//Сообщить(ТекстСообщения);
			Если Лев(Элемент.Ошибка.Описание, 14) = ""Ошибка WinHTTP"" и НЕ Кэш.РезультатОтправки.Свойство(""ПрерватьОтправку"") Тогда
				Кэш.РезультатОтправки.Вставить(""ПрерватьОтправку"", ТекстОшибки);
			КонецЕсли;
		КонецЕсли;
		сч = сч+1;
	КонецЦикла;
	фрм.ЗаписатьПараметрыДокументовСБИС(ДанныеПоСтатусам, Кэш.Ини.Конфигурация, ГлавноеОкно.КаталогНастроек);
	// << alo 
	если Кэш.парам.СостояниеЭД тогда
		фрмЭД.ЗаписатьПараметрыДокументовСБИС(ДанныеПоСтатусам, Кэш.Ини.Конфигурация, Кэш.ГлавноеОкно.КаталогНастроек);
	конецесли;	// alo >>
	
	//AU точка входа после отправки партии пакетов документов с данными по статусам.
	фрм = Кэш.ГлавноеОкно.сбисНайтиФормуФункции(""сбисОбработатьСписокОтправленных"",""РаботаСДокументами1С"","""", Кэш);
	Если Не фрм = Ложь Тогда
		//Данные по детализации статусов и списка номенклатуры смотреть в кэше.
		Контекст = Новый Структура(""ДанныеПоСтатусам"", ДанныеПоСтатусам);//Структура для возможности расширения, если понадобится добавить что-то ещё.
		фрм.сбисОбработатьСписокОтправленных(Кэш, Контекст);
	КонецЕсли;
	//
	сбисСпрятатьСостояние(ГлавноеОкно);
	
КонецФункции
&НаКлиенте
Процедура сбисПолучитьОтветыПоОтправке(Кэш) Экспорт
	Кэш.ОбщиеФункции.сбисСтатистика_СформироватьИЗаписатьСтатистикуНаСервис(Кэш, Новый Структура(""Действие"", ""Отправка""),Ложь);
КонецПроцедуры
&НаКлиенте
функция ОтправитьКаталогТоваров(Кэш,КаталогТоваров) экспорт
	// Получает структуру документа СБИС	
	МестныйКэш = Кэш;
	StreamHelper = Новый COMОбъект(""SBIS.StreamHelper"");
	param = Кэш.Docflow.CreateSimpleObject();
	
	// предварительно сохраняем в файл, чтобы указать нужную кодировку
	ТекстДок = Новый ТекстовыйДокумент;
	ТекстДок.УстановитьТекст(КаталогТоваров);
	ИмяВрФ = КаталогВременныхФайлов()+""sbisTemp.xml"";
	ТекстДок.Записать(ИмяВрФ, ""UTF-16"");
	ТекстXMLBase64 = StreamHelper.FileToBase64(ИмяВрФ);  
	Попытка 
		УдалитьФайлы(ИмяВрФ); 
	Исключение 
	КонецПопытки;
	
	param.Write( ""Файл"", ТекстXMLBase64 ); 
	Результат = Кэш.docflow.ImportNomenclatureFromCML( param );
	Если Результат = Неопределено Тогда 
		сбисСообщитьОбОшибке();
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
КонецФункции
&НаКлиенте
Функция сбисЭмитироватьКМ(Кэш, ИдДок, ДополнительныеПараметры=Неопределено, Отказ=Ложь) Экспорт
	Отказ				= Истина;
	Сообщить(""Функционал доступен только для способов обмена """"extSDK2"""" и """"API"""". Способ обмена можно изменить на вкладке """"Настройки""""."");
	Возврат Ложь;
КонецФункции
&НаКлиенте
Функция сбисЗарегистрироватьВГоссистеме(Кэш, ИдДок, ДополнительныеПараметры=Неопределено, Отказ=Ложь) Экспорт
	Отказ				= Истина;
	Сообщить(""Функционал доступен только для способов обмена """"extSDK2"""" и """"API"""". Способ обмена можно изменить на вкладке """"Настройки""""."");
	Возврат Ложь;
КонецФункции

//////////////// Вспомогательные функции/////////////////////
&НаКлиенте
Функция Включить(Кэш, ДопПараметры=Неопределено, Отказ=Ложь) Экспорт
	// Добавляет SDK в Кэш
	МестныйКэш = Кэш;
	// Убираем подключение из макета. Теперь нужно устанавливать sdk вручную.
	//ПодключитьSBISDocflow();
	Кэш.СБИС = Кэш.ГлавноеОкно.СформироватьСтруктуруКэшСБИС(Кэш.СБИС);
	Попытка
		Кэш.Вставить(""Docflow"",Новый COMОбъект(""SBIS.Docflow""));
		ВерсияSDK = Кэш.Docflow.ReadVersion();
		Кэш.СБИС.ПараметрыИнтеграции.Версия = ВерсияSDK;
		ТребуемаяВерсия = ТребуемаяВерсияКомпоненты();
		Если Кэш.ОбщиеФункции.ЭтоНоваяВерсия(ТребуемаяВерсия, ВерсияSDK) Тогда
			ПараметрыСообщения = Новый Структура;
			ПараметрыСообщения.Вставить(""АдресСсылка"", ""https://sbis.ru/help/integration/sdk/"");
			ПараметрыСообщения.Вставить(""ТекстСсылка"", ""Скачать СБИС SDK"");
			ПараметрыСообщения.Вставить(""Текст"",	""Не зарегистрирована новая версия ActiveX компоненты СБИС SDK.""+Символы.ПС+
													""Необходима версия компоненты не ниже ""+ТребуемаяВерсия+Символы.ПС+
													""Текущая версия ""+ВерсияSDK+Символы.ПС+
													""Для корректной работы установите компоненту и перезапустите 1С."");
			Кэш.ГлавноеОкно.сбисПолучитьФорму(""ФормаПредупреждения"").Показать(Кэш, ПараметрыСообщения);
			Возврат ""Не зарегистрирована новая версия ActiveX компоненты SBIS.Docflow"";	
		КонецЕсли;
		Если    ЗначениеЗаполнено(ДопПараметры)
			И	ДопПараметры.Свойство(""ВремяОжиданияОтвета"") Тогда
			сбисУстановитьВремяОжидания(Кэш, ДопПараметры.ВремяОжиданияОтвета);
		КонецЕсли;
		Кэш.СБИС.ОбменВключен = Истина;
		Кэш.СБИС.ПараметрыИнтеграции.ИнтеграцияИмя = ""SDK2"";
		Возврат Истина;
	Исключение
		Кэш.СБИС.ОбменВключен = Ложь;
		Ошибка = ОписаниеОшибки();
		ПараметрыСообщения = Новый Структура;
		ПараметрыСообщения.Вставить(""АдресСсылка"", ""https://sbis.ru/help/integration/sdk/"");
		ПараметрыСообщения.Вставить(""ТекстСсылка"", ""Скачать СБИС SDK"");
		ПараметрыСообщения.Вставить(""Текст"",	""Не зарегистрирована ActiveX компонента СБИС SDK.""+Символы.ПС+
												""Для корректной работы необходимо установить компоненту и перезапустить 1С."");

		Кэш.ГлавноеОкно.сбисПолучитьФорму(""ФормаПредупреждения"").Показать(Кэш, ПараметрыСообщения);
		Возврат ""Не зарегистрирована ActiveX компонента SBIS.Docflow"";
	КонецПопытки;
КонецФункции
&НаКлиенте
Функция Завершить(Кэш, ДопонительныеПараметры, Отказ) Экспорт
	Возврат Истина;
КонецФункции
&НаКлиенте
функция ТребуемаяВерсияКомпоненты() экспорт
	// Повышаем версию компоненты при обновлении SDK	
	Возврат ""1.2.4.0"";
КонецФункции
&НаКлиенте
Функция ПолучитьФильтр(ГлавноеОкно, ДопПараметры) Экспорт
	Возврат ГлавноеОкно.Кэш.ОбщиеФункции.ПолучитьФильтрСобытий(ГлавноеОкно.Кэш, ДопПараметры);
	//Возврат СериализоватьСтруктуруВObject(filter, ГлавноеОкно.Кэш);
КонецФункции	
&НаКлиенте
Функция СериализоватьСтруктуруВObject(Структура,Кэш) Экспорт
	// сериализует  com-объект в структуру	
	Object = Кэш.Docflow.CreateSimpleObject();
	Для Каждого Элемент Из Структура Цикл	
		Если ТипЗнч(Элемент.Значение) = Тип(""Массив"") Тогда
			ObjectList = Кэш.Docflow.CreateSimpleObjectList(); 
			Для Каждого ЭлементМассива Из Элемент.Значение Цикл
				ObjectList.add(СериализоватьСтруктуруВObject(ЭлементМассива,Кэш));	
			КонецЦикла;
			Object.WriteObjectList(Элемент.Ключ, ObjectList);	
		ИначеЕсли ТипЗнч(Элемент.Значение) = Тип(""Структура"") Тогда
			Object.WriteObject(Элемент.Ключ, СериализоватьСтруктуруВObject(Элемент.Значение,Кэш));
		ИначеЕсли ТипЗнч(Элемент.Значение) = Тип(""Дата"") Тогда
			Object.Write(Элемент.Ключ,формат(Элемент.Значение, ""ДФ='дд.ММ.гггг ЧЧ.мм.сс'""));
		Иначе
			Object.Write(Элемент.Ключ,Элемент.Значение);
		КонецЕсли;
	КонецЦикла;	
	Возврат Object;
КонецФункции
// статусы документов
//&НаСервереБезКонтекста
Функция ПолучитьРеквизитОбъекта(Объект1С, ИмяРеквизита)
	// Получает значение реквизита объекта 1С	
	Возврат Объект1С[ИмяРеквизита];	
КонецФункции
&НаКлиенте
Функция сбисПодписант(Кэш, ИНН) Экспорт
	// Получает Информацию о подписанте документа
	Возврат Новый Структура(""Должность,ФИО,ИНН"");
КонецФункции
&НаКлиенте
Функция сбисИдентификаторУчастника(Кэш, ИНН, КПП, Название) Экспорт
	// Получает Информацию о контрагенте с онлайна
	Возврат """";
КонецФункции
&НаКлиенте
Процедура сбисПрерываниеПользователем()
	// Прячет состояние при прерывании отправки, обработки служебных, обработки статусов	
	ГлавноеОкно = сбисПолучитьФорму(""ФормаГлавноеОкно"");
	сбисСпрятатьСостояние(ГлавноеОкно);	
КонецПроцедуры
&НаКлиенте
Процедура ПрерываниеПользователемОбработкиСтатусов()
	// Сохраняем параметры запроса статусов, если выполнение было прервано пользователем
	ГлавноеОкно = сбисПолучитьФорму(""ФормаГлавноеОкно"");
	Если ЕстьИзменения и ПоследнееИзменение.Свойство(""Событие"") Тогда
		КоличествоСобытий = ПоследнееИзменение.Событие.Количество();
		СтруктураНастроек = Новый Структура(""ДатаПоследнегоЗапросаСтатусов,ИдентификаторПоследнегоСобытия"", ПоследнееИзменение.Событие[КоличествоСобытий-1].ДатаВремя,ПоследнееИзменение.Событие[КоличествоСобытий-1].Идентификатор);
		МестныйКэш.ФормаНастроек.СохранитьПараметрыСБИС(МестныйКэш, СтруктураНастроек, МестныйКэш.Парам.ИдентификаторНастроек);
		ГлавноеОкно.ДатаПоследнегоЗапросаСтатусов = ПоследнееИзменение.Событие[КоличествоСобытий-1].ДатаВремя;
		ГлавноеОкно.ИдентификаторПоследнегоСобытия = ПоследнееИзменение.Событие[КоличествоСобытий-1].Идентификатор;
	КонецЕсли;
	МестныйКэш.ФормаНастроек.СохранитьПараметрыСБИС(МестныйКэш, Новый Структура(""ДатКнцЧтенияСтатусов"", сбисТекущаяДата(МестныйКэш)), МестныйКэш.Парам.ИдентификаторНастроек);
	сбисСпрятатьСостояние(ГлавноеОкно);	
КонецПроцедуры
&НаКлиенте
Функция сбисТекущаяДата(Кэш) Экспорт
	// получает текущую дату-время на сервере СБИС	
	Результат = Кэш.Docflow.ReadServerVersionInformation();	
	Если Результат = Неопределено Тогда //почему неопределено
		Возврат ТекущаяДата();
	КонецЕсли;
	Результат = СериализоватьObjectВСтруктуру(Результат);
	Возврат Результат.ВнешнийИнтерфейс.ДатаВремяЗапроса;
КонецФункции
&НаКлиенте
функция сбисТекущаяДатаМСек(Кэш) экспорт
	// Получает текущую дату в миллисекундах с начала 1970г
	Возврат 0;
КонецФункции
Функция сбисСохранитьНастройки(СтруктураНастроек) Экспорт
	// Сохраняет параметры запроса статусов в настройках пользователя СБИС	
	УстановитьПривилегированныйРежим(Истина);
	Для Каждого Элемент Из СтруктураНастроек Цикл 
		Попытка
			ХранилищеОбщихНастроек.Сохранить(Элемент.Ключ,,Элемент.Значение,,""СБИС"");
		Исключение
			ХранилищеОбщихНастроек.Сохранить(Элемент.Ключ,,Элемент.Значение);
		КонецПопытки;
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
КонецФункции
Функция сбисПолучитьНастройки(СтруктураНастроек) Экспорт
	// Получает параметры запроса статусов из настроек пользователя СБИС	
	УстановитьПривилегированныйРежим(Истина); 
	Для Каждого Элемент Из СтруктураНастроек Цикл 
		Попытка
			СтруктураНастроек[Элемент.Ключ] = ХранилищеОбщихНастроек.Загрузить(Элемент.Ключ,,,""СБИС"");
		Исключение
			СтруктураНастроек[Элемент.Ключ] = ХранилищеОбщихНастроек.Загрузить(Элемент.Ключ);
		КонецПопытки;
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	Возврат СтруктураНастроек;
КонецФункции
&НаКлиенте
Процедура УстановитьВидимостьОбновитьСтатусы(Кэш) Экспорт
	// Если более часа не проверяли статусы, то выводим красное предупреждение	
	ГлавноеОкно = Кэш.ГлавноеОкно;
	СтруктураНастроек = Кэш.ФормаНастроек.ПолучитьПараметрыСБИС(Кэш,Кэш.Парам.ИдентификаторНастроек);
	Если Не ЗначениеЗаполнено(СтруктураНастроек.ДатКнцЧтенияСтатусов) или Кэш.ВИ.сбисТекущаяДата(Кэш)-СтруктураНастроек.ДатКнцЧтенияСтатусов > 3600 Тогда
		сбисЭлементФормы(ГлавноеОкно, ""ОбновитьСтатусы"").Видимость = Истина;
	Иначе
		сбисЭлементФормы(ГлавноеОкно, ""ОбновитьСтатусы"").Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Функция ДоступныСерверныеНастройки() Экспорт
	
	Возврат	Ложь;
	
КонецФункции

////////////////////////////////////////////////////
//////////////////Автообновление////////////////////
////////////////////////////////////////////////////

&НаКлиенте
Функция сбисПолучитьПараметрыАктуальнойВерсии(Кэш, ПараметрыОбновления, Отказ) Экспорт
	
	Возврат сбисСохранитьВФайлПоСсылке(Кэш, ПараметрыОбновления, Отказ);
	
КонецФункции

&НаКлиенте
Функция сбисСохранитьВФайлПоСсылке(Кэш, сбисПараметрыФайла, Отказ) Экспорт
	
	ДопПараметрыЗапроса	= Новый Структура(""СообщатьПриОшибке, ВернутьОшибку, ЗначениеОшибки"", Ложь, Истина, 0);
	ПараметрыМетода		= Новый Структура(""uri_in, filename_in"", ,сбисПараметрыФайла.ИмяФайла);
	Если сбисПараметрыФайла.Свойство(""URL"") И Лев(сбисПараметрыФайла.URL, 8) = ""https://"" Тогда   
		ПараметрыМетода.uri_in = сбисПараметрыФайла.URL;
	ИначеЕсли Не сбисПараметрыФайла.Свойство(""URLПолный"", ПараметрыМетода.uri_in) Тогда
		ПараметрыМетода.uri_in = сбисПараметрыФайла.Протокол + ""://"" + сбисПараметрыФайла.Сервер + сбисПараметрыФайла.URL;
	КонецЕсли;
	сбисРезультатЧтения = сбисОтправитьИОбработатьКоманду(Кэш, ""LoadDataFromURIToFile"", ПараметрыМетода, ДопПараметрыЗапроса, Отказ);
	Если Отказ Тогда
		Возврат Кэш.ОбщиеФункции.сбисИсключение(сбисРезультатЧтения, ""SDK2.сбисСохранитьВФайлПоСсылке"", 700, ""Неизвестная ошибка подключения"", сбисРезультатЧтения.details);
	КонецЕсли;
	Возврат Истина;	
	
КонецФункции

////////////////////////////////////////////////////
////////////////////Авторизация/////////////////////
////////////////////////////////////////////////////

//Авторизуется на online.sbis.ru по логину/паролю	
&НаКлиенте
Функция АвторизоватьсяПоЛогинуПаролю(Кэш,Логин,Пароль,Отказ=Ложь) Экспорт 	
	МестныйКэш	= Кэш;
	ДопПараметры= Новый Структура(""СообщатьПриОшибке, ВернутьОшибку, МетодВалидации, ЗначениеОшибки"", Ложь, Истина, ""ConfirmAuth"", 0);
	СформироватьНастройкиПодключения(Кэш);
	ПараметрыРезультат = Новый Структура(	""Метод,			ДополнительныеПараметры"", 
											""Authenticate"",	ДопПараметры);
	ПараметрыРезультат.Вставить(""Результат"", СбисОтправитьИОбработатьКоманду(Кэш, ""Authenticate"", Новый Структура(""Логин,Пароль"",Логин, Пароль), ДопПараметры, Отказ));
	Возврат сбисРезультатАвторизации(Кэш, ПараметрыРезультат, Отказ);
КонецФункции

//Авторизуется на online.sbis.ru по сертификату		
&НаКлиенте
Функция АвторизоватьсяПоСертификату(Кэш,Сертификат,Отказ=Ложь) Экспорт
	МестныйКэш	= Кэш;
	сбисПараметрыВызова = Новый Структура(""Отпечаток"", Сертификат);
	
	ДопПараметры = Новый Структура(""СообщатьПриОшибке, ВернутьОшибку, МетодВалидации, ПараметрВалидации, ЗначениеОшибки"", Ложь, Истина, ""ConfirmAuthByCertificate"", сбисПараметрыВызова, 0);
	СформироватьНастройкиПодключения(Кэш);
	ПараметрыРезультат	= Новый Структура(	""Метод,						ДополнительныеПараметры"", 
											""AuthenticateByCertificate"",ДопПараметры);
	//ПараметрыРезультат.Вставить(""Результат"", Кэш.Docflow.AuthenticateByCertificate(СериализоватьСтруктуруВObject(сбисПараметрыВызова, Кэш)));
	
	ПараметрыРезультат.Вставить(""Результат"", СбисОтправитьИОбработатьКоманду(Кэш, ""AuthenticateByCertificate"", Новый Структура(""Сертификат"", сбисПараметрыВызова), ДопПараметры, Отказ));
	Возврат сбисРезультатАвторизации(Кэш, ПараметрыРезультат, Отказ);
КонецФункции

//Функция обрабатывает результат авторизации
&НаКлиенте
Функция сбисРезультатАвторизации(Кэш, ПараметрыРезультата, Отказ) Экспорт
	РезультатАвторизации = ПараметрыРезультата.Результат;
	Если Не Отказ Тогда 
		Кэш.СБИС.Авторизован = Истина;	
		Возврат ПолучитьИдентификаторСессии(Кэш);
	КонецЕсли;

	Если РезультатАвторизации.message = ""Требуется подтверждение действия"" Тогда
		//Ставим код для подтверждения действия, так как SDK его не возвращает.
		РезультатАвторизации.code = 303;
		addInfo = Новый Структура;
		addInfo.Вставить(""МетодВалидации"",	ПараметрыРезультата.ДополнительныеПараметры.МетодВалидации);
		Если ПараметрыРезультата.ДополнительныеПараметры.Свойство(""ПараметрВалидации"") Тогда
			addInfo.Вставить(""ПараметрВалидации"",	ПараметрыРезультата.ДополнительныеПараметры.ПараметрВалидации);
		КонецЕсли;
		РезультатАвторизации.Вставить(""data"", Новый Структура(""addinfo"", addinfo));
	КонецЕсли;
	Возврат Кэш.ОбщиеФункции.сбисИсключение(РезультатАвторизации, ""SDK2."" + ПараметрыРезультата.Метод); 
КонецФункции

//Подтверждаем авторизацию на online.sbis.ru по коду		
&НаКлиенте
Функция ПодтвердитьАвторизацию(Кэш, ПараметрыВвода, ПараметрыПодтверждения, Отказ) Экспорт
	ДопПараметрыЗапроса	= Новый Структура(""СообщатьПриОшибке, ВернутьОшибку, ЗначениеОшибки"", Ложь, Истина, 0);
	Парам = Новый Структура(""ПинКод"", ПараметрыВвода.ПинКод);
	Если ПараметрыПодтверждения.Свойство(""ПараметрВалидации"") Тогда
		Парам.Вставить(""Метод"", ПараметрыПодтверждения.ПараметрВалидации);
	КонецЕсли;
	Результат = сбисОтправитьИОбработатьКоманду(Кэш, ПараметрыПодтверждения.МетодВалидации, Парам, ДопПараметрыЗапроса, Отказ);
	Если Отказ Тогда
		Возврат Результат;
	Иначе
		Кэш.СБИС.Авторизован = Истина;
	КонецЕсли;
	Возврат ПолучитьИдентификаторСессии(Кэш);
КонецФункции

//Заглушка отправки кода СМС
&НаКлиенте
Функция ОтправитьКодАвторизации(Кэш, ПараметрыПодтверждения, Отказ) Экспорт
	//Для SDK отправка СМС зашита в методы авторизации.
	Возврат Неопределено;
КонецФункции

////////////////////////////////////////////////////
/////////////////Списочные методы///////////////////
////////////////////////////////////////////////////

//Получает список документов определенного типа с online.sbis.ru для реестра События 	
&НаКлиенте
Функция ПолучитьСписокДокументовОтгрузки(Кэш) Экспорт
	Отказ		= Ложь;
	МестныйКэш	= Кэш;
	ГлавноеОкно = Кэш.ГлавноеОкно;
	ГлавноеОкно.ОтметитьВсе = Ложь;
	
	сбисПоказатьСостояние(""Получение данных с "" + Кэш.СБИС.ПараметрыИнтеграции.ПредставлениеСервера, ГлавноеОкно);
	
	filter = Кэш.ОбщиеФункции.ПолучитьФильтрСобытий(Кэш, Новый Структура(""Тип"", Кэш.Текущий.ТипДок));
	Результат = СбисОтправитьИОбработатьКоманду(Кэш, ""ReadDocuments"", Новый Структура(""Фильтр"", filter), Новый Структура, Отказ);
	Если Отказ Тогда
		сбисСпрятатьСостояние(ГлавноеОкно);
		Возврат Новый Структура;
	КонецЕсли;
	Возврат Кэш.ОбщиеФункции.ОбработатьСписокДокументовОтгрузки(Кэш, Результат);
КонецФункции

//Получает статусы документов сбис
&НаКлиенте
Функция ПолучитьСписокИзменений(Кэш, ДопПараметрыФильтра = Неопределено) Экспорт
	МестныйКэш = Кэш;
	ГлавноеОкно = Кэш.ГлавноеОкно;
	фрм = ГлавноеОкно.сбисНайтиФормуФункции(""СбисЗаписатьИзмененияПоДокументам1С"",Кэш.ФормаРаботыСоСтатусами,"""",Кэш);
	// << alo 
	Если Кэш.парам.СостояниеЭД Тогда
		ПараметрыПоиска = Новый Структура;
		ПараметрыПоиска.Вставить(""ИмяФункции"",	""ЗаписатьИзмененияПоДокументам1С"");
		ПараметрыПоиска.Вставить(""КлючФорм"",	""Статусы_СостоянияЭД"");
		фрмЭД = Кэш.ОбщиеФункции.сбисНайтиФормуФункцииПодсистемы(Кэш, ПараметрыПоиска);
	КонецЕсли;
	// alo >>
	фрмПольз = ГлавноеОкно.сбисНайтиФормуФункции(""ОбработатьСписокИзменений"",""РаботаСДокументами1С"","""",Кэш);
	сбисПоказатьСостояние(""Обновление статусов отправленных документов"", ГлавноеОкно,, ""(прервать -  Ctrl+Break)"");
	
	СтруктураНастроек = Кэш.ФормаНастроек.ПолучитьПараметрыСБИС(Кэш,Кэш.Парам.ИдентификаторНастроек);
	Если (Не ЗначениеЗаполнено(СтруктураНастроек.ДатНачЧтенияСтатусов)) 
		или (ЗначениеЗаполнено(СтруктураНастроек.ДатНачЧтенияСтатусов) и ЗначениеЗаполнено(СтруктураНастроек.ДатКнцЧтенияСтатусов) и СтруктураНастроек.ДатКнцЧтенияСтатусов>=СтруктураНастроек.ДатНачЧтенияСтатусов)
		или (ЗначениеЗаполнено(СтруктураНастроек.ДатНачЧтенияСтатусов) и сбисТекущаяДата(Кэш)-СтруктураНастроек.ДатНачЧтенияСтатусов>30) Тогда
		Если Не ЗначениеЗаполнено(СтруктураНастроек.ДатаПоследнегоЗапросаСтатусов) Тогда
			СтруктураНастроек.ДатаПоследнегоЗапросаСтатусов = НачалоДня(ТекущаяДата());
		КонецЕсли;
		filter = Кэш.Docflow.CreateSimpleObject(); 
		filter.Write( ""ПолныйСертификатЭП"", ""Нет"");
		Если ЗначениеЗаполнено(СтруктураНастроек.ИдентификаторПоследнегоСобытия) Тогда
			filter.Write( ""ИдентификаторСобытия"", СтруктураНастроек.ИдентификаторПоследнегоСобытия);	
		Иначе
			filter.Write( ""ДатаВремяС"", Формат(СтруктураНастроек.ДатаПоследнегоЗапросаСтатусов,""ДФ=""""дд.ММ.гггг ЧЧ.мм.сс"""""") ); 
		КонецЕсли;
		Если ДопПараметрыФильтра <> Неопределено Тогда
			Для Каждого ДопПарам Из ДопПараметрыФильтра Цикл
				filter.Write( ДопПарам.Ключ, ДопПарам.Значение);     
			КонецЦикла;
		КонецЕсли;
		
		navigation = Кэш.Docflow.CreateSimpleObject(); 
		navigation.Write( ""РазмерСтраницы"", 100 ); 
		filter.WriteObject( ""Навигация"", navigation );
		//navigation.Write( ""ВернутьРазмерСписка"", ""Да"" );
		сч = 0;
		счДок = 0;
		оДокумент = Новый Структура;
		ЕстьИзменения = Ложь;
		ЕстьЕще = Истина;
		ПодключитьОбработчикОжидания(""ПрерываниеПользователемОбработкиСтатусов"",0.1,Истина);
		Пока ЕстьЕще Цикл
			Кэш.ФормаНастроек.СохранитьПараметрыСБИС(Кэш, Новый Структура(""ДатНачЧтенияСтатусов"", сбисТекущаяДата(Кэш)), Кэш.Парам.ИдентификаторНастроек);
			сбисПоказатьСостояние(""Обновление статусов отправленных документов ""+строка(счДок), ГлавноеОкно,, ""(прервать -  Ctrl+Break)"");
			МассивДокументов = Новый Массив;
			Если сч>0 Тогда
				Если оДокумент.Свойство(""Событие"") Тогда
					КоличествоСобытий = оДокумент.Событие.Количество();
					filter.Write( ""ИдентификаторСобытия"", оДокумент.Событие[КоличествоСобытий-1].Идентификатор);
					filter.Remove( ""ДатаВремяС"");
				Иначе
					Кэш.ФормаНастроек.СохранитьПараметрыСБИС(Кэш, Новый Структура(""ДатКнцЧтенияСтатусов"", сбисТекущаяДата(Кэш)), Кэш.Парам.ИдентификаторНастроек);
					Возврат Ложь;
				КонецЕсли;
			КонецЕсли;
			Результат = Кэш.Docflow.ReadChanges(filter);
			Если Результат = Неопределено Тогда //почему неопределено
				Если ЗначениеЗаполнено(СтруктураНастроек.ИдентификаторПоследнегоСобытия) и сч = 0 Тогда  // если событие на онлайне удалили, то пытаемся по дате запросить
					filter = Кэш.Docflow.CreateSimpleObject();
					filter.Write( ""ПолныйСертификатЭП"", ""Нет"");
					filter.Write( ""ДатаВремяС"", Формат(СтруктураНастроек.ДатаПоследнегоЗапросаСтатусов,""ДФ=""""дд.ММ.гггг ЧЧ.мм.сс"""""") );
					filter.WriteObject( ""Навигация"", navigation ); 
					Если ДопПараметрыФильтра <> Неопределено Тогда
						Для Каждого ДопПарам Из ДопПараметрыФильтра Цикл
							filter.Write( ДопПарам.Ключ, ДопПарам.Значение);     
						КонецЦикла;
					КонецЕсли;
					Результат = Кэш.Docflow.ReadChanges(filter);
					Если Результат = Неопределено Тогда
						сбисСпрятатьСостояние(ГлавноеОкно);
						сбисСообщитьОбОшибке();
						Кэш.ФормаНастроек.СохранитьПараметрыСБИС(Кэш, Новый Структура(""ДатКнцЧтенияСтатусов"", сбисТекущаяДата(Кэш)), Кэш.Парам.ИдентификаторНастроек);
						Возврат Ложь;
					КонецЕсли;	
				Иначе	
					сбисСпрятатьСостояние(ГлавноеОкно);
					сбисСообщитьОбОшибке();
					Кэш.ФормаНастроек.СохранитьПараметрыСБИС(Кэш, Новый Структура(""ДатКнцЧтенияСтатусов"", сбисТекущаяДата(Кэш)), Кэш.Парам.ИдентификаторНастроек);
					Возврат Ложь;
				КонецЕсли;
			КонецЕсли;
			сч = сч + 1;
			СписокИзменений = СериализоватьObjectВСтруктуру(Результат);
			ЕстьЕще = СписокИзменений.Навигация.ЕстьЕще=""Да"";
			Для Каждого оДокумент Из СписокИзменений.Документ Цикл
				счДок = счДок + 1;
				ЕстьИзменения = Истина;
				сбисПоказатьСостояние(""Обновление статусов отправленных документов ""+строка(счДок), ГлавноеОкно,, ""(прервать -  Ctrl+Break)"");
				МассивДокументов.Добавить(оДокумент);
				Если Кэш.ИспользоватьОбработчикиСобытий = Истина Тогда
					ИмяОбработчика = СтрЗаменить(""сбис""+оДокумент.Регламент.Название+""_""+оДокумент.Событие[0].Название, "" "", ""_"");
					фрмОбработчика = Кэш.ГлавноеОкно.сбисНайтиФормуФункции(ИмяОбработчика,""РаботаСДокументами1С"","""",Кэш);
					Если фрмОбработчика <> Ложь Тогда
						Контекст = Новый структура(""Док, Кэш"", оДокумент, Кэш);
						Результат=вычислить(""фрмОбработчика.""+ИмяОбработчика+""(Контекст)"");
					КонецЕсли;	
				КонецЕсли;
			КонецЦикла;
			сбисПоказатьСостояние(""Обновление статусов отправленных документов ""+строка(счДок), ГлавноеОкно,, ""(прервать -  Ctrl+Break)"");
			ПоследнееИзменение = оДокумент;
			//сбисПоказатьСостояние(""Запись статусов в регистр сведений"", ГлавноеОкно);
			фрм.СбисЗаписатьИзмененияПоДокументам1С(Кэш, МассивДокументов, Новый Структура);
			// << alo 
			если кэш.парам.СостояниеЭД тогда
				фрмЭД.ЗаписатьИзмененияПоДокументам1С(МассивДокументов, Кэш.Ини, ГлавноеОкно.КаталогНастроек);
			конецесли; // alo >>
			Если фрмПольз<>Ложь Тогда
				фрмПольз.ОбработатьСписокИзменений(МассивДокументов, Кэш);	
			КонецЕсли;
			Кэш.ФормаНастроек.СохранитьПараметрыСБИС(Кэш, Новый Структура(""ДатКнцЧтенияСтатусов"", сбисТекущаяДата(Кэш)), Кэш.Парам.ИдентификаторНастроек);
			ОбработкаПрерыванияПользователя();
		КонецЦикла;	
		Если ЕстьИзменения и оДокумент.Свойство(""Событие"") Тогда
			КоличествоСобытий = оДокумент.Событие.Количество();
			СтруктураНастроек = Новый Структура(""ДатаПоследнегоЗапросаСтатусов,ИдентификаторПоследнегоСобытия"", оДокумент.Событие[КоличествоСобытий-1].ДатаВремя,оДокумент.Событие[КоличествоСобытий-1].Идентификатор);
			Кэш.ФормаНастроек.СохранитьПараметрыСБИС(Кэш, СтруктураНастроек, Кэш.Парам.ИдентификаторНастроек);
			ГлавноеОкно.ДатаПоследнегоЗапросаСтатусов = оДокумент.Событие[КоличествоСобытий-1].ДатаВремя;
			ГлавноеОкно.ИдентификаторПоследнегоСобытия = оДокумент.Событие[КоличествоСобытий-1].Идентификатор;
		КонецЕсли;
	Иначе
		Сообщить(""Обновление статусов уже выполняется по запросу другого пользователя."");
	КонецЕсли;
	сбисСпрятатьСостояние(ГлавноеОкно);
	ОтключитьОбработчикОжидания(""ПрерываниеПользователемОбработкиСтатусов"");
КонецФункции

//Получает список документов по событиям с online.sbis.ru
&НаКлиенте
Функция ПолучитьСписокСобытий(Кэш, ТипРеестра) Экспорт
	МестныйКэш = Кэш;
	ГлавноеОкно = Кэш.ГлавноеОкно;
	сбисПоказатьСостояние(""Получение данных с "" + Кэш.СБИС.ПараметрыИнтеграции.ПредставлениеСервера, ГлавноеОкно);
	
	ГлавноеОкно.ОтметитьВсе = Ложь;
	
	filter = Кэш.ОбщиеФункции.ПолучитьФильтрСобытий(Кэш, Новый Структура(""ТипРеестра"", ТипРеестра));
	Возврат ПолучитьСписокСобытийПоФильтру(Кэш, filter, ГлавноеОкно);
КонецФункции

//Получает список документов по событиям с online.sbis.ru	
&НаКлиенте
Функция ПолучитьСписокСобытийПоФильтру(Кэш, filter, ГлавноеОкно) Экспорт
	Отказ = Ложь;
	МестныйКэш = Кэш;
	Результат = СбисОтправитьИОбработатьКоманду(Кэш, ""ReadDocumentsByEvents"", Новый Структура(""Фильтр"", filter), Новый Структура, Отказ);
	Если Отказ Тогда
		сбисСпрятатьСостояние(ГлавноеОкно);
		Возврат Новый Структура;
	КонецЕсли;
	Возврат Кэш.ОбщиеФункции.ОбработатьСписокСобытий(Кэш, Результат);
КонецФункции

//Получает список организаций, присутствующих одновременно в кабинете СБИС и в базе 1С, у которых есть необработанные служебные документы
&НаКлиенте
Функция ПолучитьСписокНашихОрганизаций(Кэш, СписокИНН) Экспорт
	МестныйКэш = Кэш;
	ЕстьЕще = Истина;
	ТекущаяСтраница = 0;
	МассивОрганизаций = Новый Массив;
	Пока ЕстьЕще Цикл
		filter  = МестныйКэш.Docflow.CreateSimpleObject();
		navigation = МестныйКэш.Docflow.CreateSimpleObject();
		navigation.Write( ""Страница"", ТекущаяСтраница);
		filter.WriteObject( ""Навигация"", navigation );
		Результат = Кэш.Docflow.ReadOurOrganizations(filter); // Получаем список наших организаций
		Если Результат = Неопределено Тогда //почему неопределено
			сбисСообщитьОбОшибке();
			Возврат МассивОрганизаций;
		КонецЕсли;
		Выборка = Результат.ReadObjectList(""НашаОрганизация"");
		Размер = Выборка.count;
		СтруктураОрганизаций = Новый Структура;
		Для сч=0 По Размер-1 Цикл
			Запись = Выборка.at(сч);
			оОрганизация = СериализоватьObjectВСтруктуру(Запись);	
			Организация1С = Ложь;
			Организация1С = МестныйКэш.ОбщиеФункции.НайтиОрганизациюИзДокументаСБИС(МестныйКэш.Ини.Конфигурация, оОрганизация);
			Если Организация1С<>Ложь Тогда
				оОрганизация.Вставить(""com"", Запись);
				оОрганизация.Вставить(""Название"", строка(Организация1С));
				
				МассивОрганизаций.Добавить(оОрганизация);
				// добавляем ИНН в список для активации серверных ключей
				Если оОрганизация.Свойство(""СвФЛ"") Тогда
					Если СписокИНН.НайтиПоЗначению(оОрганизация.СвФЛ.ИНН)=Неопределено Тогда 
						СписокИНН.Добавить(оОрганизация.СвФЛ.ИНН);
					КонецЕсли;
				Иначе
					Если СписокИНН.НайтиПоЗначению(оОрганизация.СвЮЛ.ИНН)=Неопределено Тогда
						СписокИНН.Добавить(оОрганизация.СвЮЛ.ИНН);
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
		ТекущаяСтраница = ТекущаяСтраница + 1;
		ЕстьЕще = Результат.ReadObject(""Навигация"").Read(""ЕстьЕще"")=""Да"";
	КонецЦикла;
	Возврат МассивОрганизаций;
КонецФункции

//Получает список документов определенного типа с online.sbis.ru	
&НаКлиенте
Функция СбисПолучитьСписокДокументов(Кэш) Экспорт
	МестныйКэш = Кэш;
	ГлавноеОкно = Кэш.ГлавноеОкно;
	сбисПоказатьСостояние(""Получение данных с "" + Кэш.СБИС.ПараметрыИнтеграции.ПредставлениеСервера, ГлавноеОкно);
	
	ГлавноеОкно.ОтметитьВсе = Ложь;
	
	filter = ПолучитьФильтр(ГлавноеОкно, Новый Структура(""Тип"", Кэш.Текущий.ТипДок));
	
	Возврат СбисПолучитьСписокДокументовПоФильтру(Кэш, filter, ГлавноеОкно); 
КонецФункции

//Получает список документов определенного типа с online.sbis.ru
&НаКлиенте
Функция СбисПолучитьСписокДокументовПоФильтру(Кэш, filter, ГлавноеОкно) Экспорт	
	Отказ = Ложь;
	Результат = СбисОтправитьИОбработатьКоманду(Кэш, ""ReadDocuments"", Новый Структура(""Фильтр"", filter), Новый Структура, Отказ);
	Если Отказ Тогда
		сбисСпрятатьСостояние(ГлавноеОкно);
		Возврат Новый Структура;
	КонецЕсли;
	Возврат Кэш.ОбщиеФункции.ОбработатьСписокДокументов(Кэш, Результат);
КонецФункции

////////////////////////////////////////////////////
//////////////////Статистика вызов//////////////////
////////////////////////////////////////////////////

//Отправляет собщение о статистике на online
&НаКлиенте
Функция СбисОтправитьСообщениеСтатистики(Кэш, СообщениеСтатистики, Отказ) Экспорт
	сбисДополнительныеПараметрыКоманды = Новый Структура(""ВернутьОшибку, ЗначениеОшибки"", Истина, Неопределено);
	Возврат сбисОтправитьИОбработатьКоманду(Кэш, ""WriteStat"", Новый Структура(""message"", СообщениеСтатистики), сбисДополнительныеПараметрыКоманды, Отказ);
КонецФункции

//Отправляет собщение о ошибке на online
&НаКлиенте
Функция СбисОтправитьСообщениеОшибки(Кэш, СообщениеОбОшибке, Отказ) Экспорт
	сбисДополнительныеПараметрыКоманды = Новый Структура(""СообщатьПриОшибке, ВернутьОшибку, ЗначениеОшибки"", Ложь, Истина, Неопределено);
	Возврат сбисОтправитьИОбработатьКоманду(Кэш, ""WriteError"", Новый Структура(""error"", СообщениеОбОшибке), сбисДополнительныеПараметрыКоманды, Отказ);
КонецФункции

&НаКлиенте
Функция сбисПроверкаОбновления(Кэш, ИнформацияОТекущейВерсии, Отказ) Экспорт
	ДопПараметры = Новый Структура(""ВернутьОшибку"", Ложь);
	Возврат сбисОтправитьИОбработатьКоманду(Кэш, ""GetProductVersionStatus"", ИнформацияОТекущейВерсии, ДопПараметры, Отказ);
КонецФункции

////////////////////////////////////////////////////
/////////////////////Системные//////////////////////
////////////////////////////////////////////////////

&НаКлиенте
Функция СбисВключенРезервныйДомен(Кэш, АдресСервера) Экспорт
	Возврат Ложь;
КонецФункции

//Выполняет указанный метод и возвращает результат, либо генерирует и сообщает ошибку.
//ПараметрыМетода			- структура, на ключи не смотрим, сделано для совместимости с extSDK, API.
//Дополнительные параметры	- структура, для управления выводом и возвратом результата в случае ошибок. 
//	Возможные значения: СообщатьПриОшибке(Истина),ВернутьОшибку(Ложь) 
//Отказ - булево, определяет наличие ошибок в процессе выполнения метода и что вернулось в качестве результата. Если Истина, то структура ошибки с полями ""code, message, details""
&НаКлиенте
Функция СбисОтправитьИОбработатьКоманду(Кэш, Метод, ПараметрыМетода=Неопределено, ДопПараметры, Отказ) Экспорт
	Перем сбисРезультат;
	сбисСтрокаАргументы	= """";
	сбисМассивАргументов= Новый Массив;
	сбисПараметрыВызова	= Новый Структура(""Метод, Параметры"", Метод, ПараметрыМетода);	
	Если Не ПараметрыМетода = Неопределено Тогда
		сбисСчетчик = 0;
		Для Каждого КлючИЗначение Из ПараметрыМетода Цикл
			Попытка
				сбисМассивАргументов.Добавить(ПреобразоватьЗначениеВCOM(Кэш, КлючИЗначение.Значение, Ложь).Object);
			Исключение
				Отказ = Истина;
				сбисРезультат = Кэш.ОбщиеФункции.сбисИсключение(, ""SDK2.ПреобразоватьЗначениеВCOM"", 700, ""Неизвестная ошибка подключения"", сбисТекстИсключенияПриКонвертации(""Струкутра"", ИнформацияОбОшибке().Описание, КлючИЗначение.Ключ, Истина));
				Прервать;
			КонецПопытки;
			сбисСтрокаАргументы = сбисСтрокаАргументы + "",сбисМассивАргументов["" + сбисСчетчик + ""]"";
			сбисСчетчик = сбисСчетчик + 1;
		КонецЦикла;
		сбисСтрокаАргументы = Сред(сбисСтрокаАргументы, 2);
	КонецЕсли;
	//Выполняем метод если все успешно преобразовано
	Если Не Отказ Тогда
		Попытка
			сбисРезультат = Вычислить(""Кэш.Docflow."" + Метод + ""("" + сбисСтрокаАргументы + "")"");
		Исключение
			Отказ = Истина;
			сбисРезультат = Кэш.ОбщиеФункции.сбисИсключение(, ""SDK2.сбисОтправитьИОбработатьКоманду"", 700, ""Неизвестная ошибка подключения"", ОписаниеОшибки()); 
		КонецПопытки;
	КонецЕсли;
	Возврат сбисОбработатьОтвет(Кэш, сбисПараметрыВызова, сбисРезультат, ДопПараметры, Отказ);
	
КонецФункции

&НаКлиенте
Функция СбисОбработатьОтвет(Кэш, сбисПараметрыВызова, сбисРезультат, сбисДополнительныеПараметры, Отказ)
	Перем checkparam, message_result, сбисПроверкаНаОшибку; 
	
	Если Не сбисДополнительныеПараметры.Свойство(""ЗначениеОшибки"", сбисПроверкаНаОшибку) Тогда
		//В большинстве случаев для SDK неудачей является Неопределено.
		сбисПроверкаНаОшибку = Неопределено;			
	КонецЕсли;
	
	Если Отказ Тогда//Уже где-то отвалилось с ошибкой.
		message_result = сбисРезультат;
	ИначеЕсли сбисРезультат = сбисПроверкаНаОшибку Тогда//Смотрим ошибку от SDK
		Отказ = Истина;
		message_result = Кэш.ОбщиеФункции.сбисИсключение(сбисПолучитьСтруктуруОшибки(Кэш), сбисПараметрыВызова.Метод);
	Иначе
		message_result = сбисПрочитатьОтвет(Кэш, сбисРезультат, Отказ);
	КонецЕсли;
					
	Если Отказ Тогда
		Ошибка = message_result;
		Если Не сбисДополнительныеПараметры.Свойство(""СообщатьПриОшибке"", checkparam)
			Или	checkparam Тогда
			Кэш.ГлавноеОкно.сбисСообщитьОбОшибке(Кэш, Ошибка);
		КонецЕсли;
		Если 	сбисДополнительныеПараметры.Свойство(""ВернутьОшибку"", checkparam)
			И	checkparam Тогда
			Возврат Ошибка; 
		КонецЕсли;
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат	message_result;	
	
КонецФункции

&НаКлиенте
Функция СбисПрочитатьОтвет(Кэш, сбисРезультат, Отказ)	
	Попытка
		Результат = ПреобразоватьComВЗначение(сбисРезультат,Отказ);
	Исключение
		Отказ = Истина;
		Результат = Кэш.ОбщиеФункции.сбисИсключение(, ""SDK2.ПреобразоватьComВЗначение"", 766, ""Ошибка при конвертации"", сбисТекстИсключенияПриКонвертации(""Структура"", ИнформацияОбОшибке().Описание, ""SimpleObject"", Истина));
	КонецПопытки;
	Возврат Результат;
КонецФункции

//Фунция вызывает исключение с генерацией пути до элемента
&НаКлиенте
Функция СбисТекстИсключенияПриКонвертации(сбисТип="""", сбисТекстОшибки, сбисОписаниеПути, ВерхнийУровень=Ложь)
	сбисИндексВОшибке = Найти(сбисТекстОшибки, ""%Заменить%"");
	Если сбисИндексВОшибке Тогда
		ЗначениеПосле = Сред(сбисТекстОшибки, сбисИндексВОшибке + 10, 1);
		Если	Не ЗначениеПосле = ""[""
			И	Не ЗначениеПосле = "".""
			И	Не ПустаяСтрока(сбисОписаниеПути) Тогда
			сбисОписаниеПути = сбисОписаниеПути + ""."";
		КонецЕсли;
		Возврат СтрЗаменить(сбисТекстОшибки, ""%Заменить%"", ?(ВерхнийУровень, "" "", ""%Заменить%"") + сбисОписаниеПути);
	КонецЕсли;
	сбисОшибка = ""значения"";
	Если		сбисТип = ""Массив""		Тогда
		сбисСтрока = ""элемента массива"";
	ИначеЕсли	сбисТип = ""Структура""	Тогда
		сбисСтрока = ""узла"";
	КонецЕсли;
	Возврат ""Ошибка конвертации "" + сбисСтрока + ?(ВерхнийУровень, "" "", ""%Заменить%"") + сбисОписаниеПути + "". Детально: "" + сбисТекстОшибки;
		
КонецФункции

//Cериализует SimpleObject SDK в структуру 1С
&НаКлиенте
Функция СериализоватьObjectВСтруктуру(Object) Экспорт
	Попытка
		ComКлючиОбъекта	= Object.keys;
	Исключение
		Сообщить(""Ошибка при работе с SimpleObject. У объекта отсутствует описание ключей. Детально: "" + ИнформацияОбОшибке().Причина);
		Возврат Неопределено;
	КонецПопытки;
	сбисСтруктура	= Новый Структура();
	Для Каждого ИмяОбъекта Из ComКлючиОбъекта Цикл
		Тип = Object.TypeOf(ИмяОбъекта);	
		Если Тип = ""строка"" Тогда
			Строка = Object.Read(ИмяОбъекта);
			Если Сред(Строка,3,1)=""."" и Сред(Строка,6,1)=""."" и (СтрДлина(Строка)=10 или СтрДлина(Строка)=19) Тогда //видимо это дата
				Попытка
					Если СтрДлина(Строка)=10 Тогда
						Строка = Дата(Сред(Строка,7,4), Сред(Строка,4,2), Лев(Строка, 2));
					Иначе
						Строка = Дата(Сред(Строка,7,4), Сред(Строка,4,2), Лев(Строка, 2), Сред(Строка,12, 2), Сред(Строка,15, 2), Сред(Строка,18, 2))
					КонецЕсли;
				Исключение
				КонецПопытки;
			КонецЕсли;
			сбисСтруктура.Вставить(ИмяОбъекта,Строка);
		ИначеЕсли Тип = ""объект"" Тогда
			сбисСтруктура.Вставить(ИмяОбъекта,СериализоватьObjectВСтруктуру(Object.ReadObject(ИмяОбъекта)));
		ИначеЕсли Тип = ""массив объектов"" Тогда
			сбисСтруктура.Вставить(ИмяОбъекта,Новый Массив());
			ObjectList = Object.ReadObjectList(ИмяОбъекта);
			ObjectListCount = ObjectList.count;
			Для сч = 0 По ObjectListCount - 1 Цикл
				сбисСтруктура[ИмяОбъекта].Добавить(СериализоватьObjectВСтруктуру(ObjectList.at(сч)));
			КонецЦикла;	
		Иначе
			Сообщить(""Обратитесь в техподдержку. Появился новый тип Object ""+Тип);
		КонецЕсли;		
	КонецЦикла;
	Возврат сбисСтруктура;		
КонецФункции
	
//Получает последнюю ошибку SDK в стандартной структуре ошибок для всех способов обмена.	
&НаКлиенте
Функция СбисПолучитьСтруктуруОшибки(Кэш) Экспорт
	Ошибка = Новый Структура(""code,message,details"");
	Error = Кэш.Docflow.ReadLastErrorEx();
	Попытка
		Ошибка.message	= Error.Read(""Описание"");
		Ошибка.details	= Error.Read(""ОписаниеРасширенное"");
		Ошибка.code		= Error.Read(""Код"");
	Исключение
		Ошибка.message	= ""Неизвестная ошибка системы"";
		Ошибка.details	= ОписаниеОшибки();
		Ошибка.code		= 100;
	КонецПопытки;
	Попытка
		Ошибка.code		= Число(Ошибка.code);
	Исключение
		Ошибка.code		= 100;
	КонецПопытки;

	Возврат Ошибка;
КонецФункции

#Область include_core_vo2_СпособыОбмена_SDK2_Вызовы

&НаКлиенте
Функция СбисВыполнитьДействие(Кэш, СоставПакета, Этап, Действие, Комментарий, ПредставлениеПакета) Экспорт
	Перем Сертификат;
	
	МестныйКэш	= Кэш;
	Отказ		= Ложь;
	
	Если	Действие.Свойство(""Сертификат"")
		И	Действие.Сертификат.Количество() Тогда
		Сертификат = Действие.Сертификат[0];
	КонецЕсли;
	Если Сертификат = Неопределено И Действие.ТребуетПодписания = ""Да"" Тогда
		Кэш.ГлавноеОкно.СбисСообщитьОбОшибке(Кэш, Кэш.ОбщиеФункции.СбисИсключение(, ""SDK2.СбисВыполнитьДействие"", 755,, ""Отсутствует сертификат ЭЦП для утверждения/отклонения пакета документов "" + ПредставлениеПакета));
		Возврат Ложь;
	КонецЕсли;
	
	ОбработатьСлужебныеДокументыПоПакету(Кэш, СоставПакета);
	
	Для Каждого Вложение Из СоставПакета.Вложение Цикл
		Если Вложение.Свойство(""Зашифрован"") И Вложение.Зашифрован = ""Да"" Тогда
			Кэш.ГлавноеОкно.СбисСообщитьОбОшибке(Кэш, Кэш.ОбщиеФункции.СбисИсключение(, ""SDK2.СбисВыполнитьДействие"", 601, ""Для корректной обработки зашифрованных документов выберите способ обмена """"extSDK"""" на вкладке Настройки.""));
			Возврат Ложь;
		КонецЕсли;
		Прервать;
	КонецЦикла;	
	
	Результат = Кэш.ОбщиеФункции.СбисВыполнитьДействие(Кэш, Новый Структура(""СоставПакета, Этап, Действие, Комментарий, Сертификат"", СоставПакета, Этап, Действие, Комментарий, Сертификат), Отказ);
	Если Отказ Тогда
		Кэш.ГлавноеОкно.СбисСообщитьОбОшибке(Кэш, Результат);
		Возврат Ложь;
	КонецЕсли;
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область include_core_vo2_СпособыОбмена_SDK2_ВнешниеОбертки

&НаКлиенте
Функция СБИС_ПодготовитьДействие(Кэш, document_in, ДопПараметры, Отказ) Экспорт
	Результат = СбисОтправитьИОбработатьКоманду(Кэш, ""PrepareAction"", Новый Структура(""document_in"", document_in), ДопПараметры, Отказ);
	Если Отказ Тогда
		Возврат Кэш.ОбщиеФункции.сбисИсключение(Результат,  ""SDK2.СБИС_ПодготовитьДействие"");
	КонецЕсли;
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция СБИС_ВыполнитьДействие(Кэш, document_in, ДопПараметры, Отказ) Экспорт
	результат = СбисОтправитьИОбработатьКоманду(Кэш, ""ExecuteAction"", Новый Структура(""document_in"", document_in), ДопПараметры, Отказ);	
	Если Отказ Тогда
		Возврат Кэш.ОбщиеФункции.сбисИсключение(Результат,  ""SDK2.СБИС_ВыполнитьДействие"");
	КонецЕсли;
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция СБИС_СериализоватьСтрокуВBase64(Кэш, ПараметрыСериализовать, ДопПараметры, Отказ) Экспорт
	Попытка
		StreamHelper = Новый COMОбъект(""SBIS.StreamHelper"");
		Возврат StreamHelper.StringToBase64(ПараметрыСериализовать.Строка); 
	Исключение
		Отказ = Истина;
		ОшибкаСериализации = ИнформацияОбОшибке();
		Возврат Кэш.ОбщиеФункции.сбисИсключение(ОшибкаСериализации, ""SDK2.СБИС_ВыполнитьДействие"");
	КонецПопытки;
КонецФункции

&НаКлиенте
Функция СБИС_СохранитьПоСсылкеВФайл(Кэш, ПараметрыФайла, ДопПараметры, Отказ) Экспорт
	ДопПараметрыЗапроса	= Новый Структура(""СообщатьПриОшибке, ВернутьОшибку, ЗначениеОшибки"", Ложь, Истина, 0);
	ПараметрыЗаписи = Новый Структура(""uri_in, filename_in"", ПараметрыФайла.Ссылка, ПараметрыФайла.ИмяФайла);
	
	Результат = СбисОтправитьИОбработатьКоманду(Кэш, ""LoadDataFromURItoFile"", ПараметрыЗаписи, ДопПараметрыЗапроса, Отказ);
	Если Отказ Тогда 
		Возврат Кэш.ОбщиеФункции.сбисИсключение(Результат,  ""SDK2.СБИС_СохранитьПоСсылкеВФайл"");
	КонецЕсли;
	Возврат ПараметрыФайла.ИмяФайла;
КонецФункции

&НаКлиенте
Функция СБИС_ИнформацияОКонтрагенте(Кэш, СтруктураКонтрагента, ДопПараметры, Отказ) Экспорт
	Результат = СбисОтправитьИОбработатьКоманду(Кэш, ""ReadContragentInfo"", Новый Структура(""Участник"", СтруктураКонтрагента), ДопПараметры, Отказ);
	Если Отказ Тогда
		Возврат Кэш.ОбщиеФункции.сбисИсключение(Результат,  Кэш.СБИС.ПараметрыИнтеграции.ИнтеграцияИмя + "".СБИС_ИнформацияОКонтрагенте"");
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция СБИС_ПолучитьИнформациюОТекущемПользователе(Кэш, param, ДопПараметры, Отказ) Экспорт
	Результат = СбисОтправитьИОбработатьКоманду(Кэш, ""ReadCurrentUserInfo"", param, ДопПараметры, Отказ);
	Если Отказ Тогда
		Возврат Кэш.ОбщиеФункции.сбисИсключение(Результат, Кэш.СБИС.ПараметрыИнтеграции.ИнтеграцияИмя + "".СБИС_ПолучитьИнформациюОТекущемПользователе"");
	КонецЕсли;
	Возврат Результат;
КонецФункции 

&НаКлиенте
Функция СБИС_ПолучитьСписокАккаунтов(Кэш, param, ДопПараметры, Отказ) Экспорт
	Результат = СбисОтправитьИОбработатьКоманду(Кэш, ""AccountsList"", param, ДопПараметры, Отказ);
	Если Отказ Тогда
		Возврат Кэш.ОбщиеФункции.сбисИсключение(Результат, ""SDK2.СБИС_ПолучитьСписокАккаунтов"");
	КонецЕсли;
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция СБИС_ПереключитьАккаунт(Кэш, param, ДопПараметры, Отказ) Экспорт 
	
	ДопПараметрыВызова= Новый Структура(""СообщатьПриОшибке, ВернутьОшибку, ЗначениеОшибки"", Ложь, Истина, 0);
	ПараметрыРезультат = Новый Структура(	""Метод,			ДополнительныеПараметры"", 
											""SwitchAccount"",	ДопПараметрыВызова);
	ПараметрыРезультат.Вставить(""Результат"", СбисОтправитьИОбработатьКоманду(Кэш, ""SwitchAccount"", Новый Структура(""Параметр"", param), ДопПараметрыВызова, Отказ));
	Возврат сбисРезультатАвторизации(Кэш, ПараметрыРезультат, Отказ);
КонецФункции


#КонецОбласти

#Область include_core_vo2_СпособыОбмена_SDK2_Сериализатор

&НаКлиенте
Функция ПреобразоватьComВЗначение(сбисРезультат, Отказ, сбисТип=Неопределено)
	РезультатКовертации = Неопределено;
	//Первый заход, смотрим что в ответе.	
	Если сбисТип = Неопределено Тогда
		Если сбисРезультат = Неопределено Тогда
			РезультатКовертации = сбисРезультат;			
		ИначеЕсли ТипЗнч(сбисРезультат) = Тип(""COMОбъект"") Тогда
			ТипПреобразовать = ""объект"";
			Попытка
				РезультатПроверки = сбисРезультат.keys;
			Исключение
				ТипПреобразовать = ""массив объектов"";
			КонецПопытки;
			РезультатКовертации = ПреобразоватьComВЗначение(сбисРезультат, Отказ, ТипПреобразовать);
		ИначеЕсли ТипЗнч(сбисРезультат) = Тип(""Строка"") Тогда
			РезультатКовертации = ПреобразоватьComВЗначение(сбисРезультат, Отказ, ""строка"");
		Иначе
			РезультатКовертации = сбисРезультат;
		КонецЕсли;
	ИначеЕсли сбисТип = ""строка"" Тогда
		РезультатКовертации = сбисРезультат;
		Если	Сред(РезультатКовертации,3,1) = "".""
			И	Сред(РезультатКовертации,6,1) = ""."" Тогда//Возможно, это дата
			//Переопределим значение как дату, если это действительно она
			Если		СтрДлина(РезультатКовертации) = 10 Тогда
				Попытка
					РезультатКовертации = Дата(Сред(РезультатКовертации,7,4), Сред(РезультатКовертации,4,2), Лев(РезультатКовертации, 2));
				Исключение
					//В случае исключения, ничего не делаем. Это не дата, значение не меняется
				КонецПопытки;
			ИначеЕсли	СтрДлина(РезультатКовертации) = 19 Тогда 
				Попытка
					РезультатКовертации = Дата(Сред(РезультатКовертации,7,4), Сред(РезультатКовертации,4,2), Лев(РезультатКовертации, 2), Сред(РезультатКовертации,12, 2), Сред(РезультатКовертации,15, 2), Сред(РезультатКовертации,18, 2))
				Исключение
					//В случае исключения, ничего не делаем. Это не дата, значение не меняется
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли сбисТип = ""объект"" Тогда
		Попытка
			ComКлючиОбъекта	= сбисРезультат.keys;
		Исключение
			ОписаниеОшибки = ""У объекта отсутствует описание ключей."";
			ВызватьИсключение сбисТекстИсключенияПриКонвертации(""Структура"", ОписаниеОшибки, """");
		КонецПопытки;
		РезультатКовертации = Новый Структура();
		Для Каждого ИмяОбъекта Из ComКлючиОбъекта Цикл
			ТипПреобразовать = сбисРезультат.TypeOf(ИмяОбъекта);
			Если		ТипПреобразовать = ""строка"" Тогда
				ЗначениеПреобразовать = сбисРезультат.Read(ИмяОбъекта);
			ИначеЕсли	ТипПреобразовать = ""объект"" Тогда
				ЗначениеПреобразовать = сбисРезультат.ReadObject(ИмяОбъекта);
			ИначеЕсли	ТипПреобразовать = ""массив объектов"" Тогда
				ЗначениеПреобразовать = сбисРезультат.ReadObjectList(ИмяОбъекта);
			Иначе
				ВызватьИсключение сбисТекстИсключенияПриКонвертации(""Структура"", ""Неизвестный тип ответа "" + ТипПреобразовать + "". Обратитесь в техническую поддержку."", ИмяОбъекта);
			КонецЕсли;
			Попытка
				РезультатКовертации.Вставить(ИмяОбъекта, ПреобразоватьComВЗначение(ЗначениеПреобразовать, Отказ, ТипПреобразовать));
			Исключение
				ОписаниеОшибки = ИнформацияОбОшибке().Описание;
				ВызватьИсключение сбисТекстИсключенияПриКонвертации(""Структура"", ОписаниеОшибки, ИмяОбъекта);
			КонецПопытки;
		КонецЦикла;
	ИначеЕсли сбисТип = ""массив объектов"" Тогда
		РезультатКовертации = Новый Массив();
		Для сч = 0 По сбисРезультат.count - 1 Цикл
			Попытка
				РезультатКовертации.Добавить(ПреобразоватьComВЗначение(сбисРезультат.at(сч), Отказ, ""объект""));
			Исключение
				ОписаниеОшибки = ИнформацияОбОшибке().Описание;
				ВызватьИсключение сбисТекстИсключенияПриКонвертации(""Массив"", ОписаниеОшибки, ""["" + сч + ""]"");
			КонецПопытки;
		КонецЦикла;	
	КонецЕсли;		
	Возврат РезультатКовертации;
		
КонецФункции

//Cериализует произвольный объект 1С в аналогичную стркутуру SDK
//Поддерживаемые типы: массив, структура, дата. Все прочие приводятся к строке.
&НаКлиенте
Функция ПреобразоватьЗначениеВCOM(Кэш, ЗначениеПреобразовать, ВерхнийУровень=Истина)
	Результат = Новый Структура(""Type, Object"", ""Строка"");
	Если ТипЗнч(ЗначениеПреобразовать) = Тип(""Массив"") Тогда
		Результат.Object = Кэш.Docflow.CreateSimpleObjectList(); 
		Результат.Type = ""Массив"";
		Для сбисИндексМассива = 0 По ЗначениеПреобразовать.Количество() - 1 Цикл
			Попытка
				ЭлементДобавить = ПреобразоватьЗначениеВCOM(Кэш, ЗначениеПреобразовать[сбисИндексМассива], Ложь);
				Результат.Object.Add(ЭлементДобавить.Object);	
			Исключение
				ОписаниеОшибки = ИнформацияОбОшибке().Описание;
				ВызватьИсключение сбисТекстИсключенияПриКонвертации(Результат.Type, ОписаниеОшибки, ""["" + сбисИндексМассива + ""]"", ВерхнийУровень);
			КонецПопытки;
		КонецЦикла;
	ИначеЕсли ТипЗнч(ЗначениеПреобразовать) = Тип(""Структура"") Тогда
		Результат.Object = Кэш.Docflow.CreateSimpleObject(); 
		Результат.Type = ""Структура"";
		Для Каждого КлючИЗначение Из ЗначениеПреобразовать Цикл
			сбисЗначениеCOM = ПреобразоватьЗначениеВCOM(Кэш, КлючИЗначение.Значение, Ложь);
			Попытка
				Если сбисЗначениеCOM.Type = ""Массив"" Тогда
					Результат.Object.WriteObjectList(КлючИЗначение.Ключ, сбисЗначениеCOM.Object);		
				ИначеЕсли сбисЗначениеCOM.Type = ""Структура"" Тогда
					Результат.Object.WriteObject(КлючИЗначение.Ключ, сбисЗначениеCOM.Object);
				Иначе
					Результат.Object.Write(КлючИЗначение.Ключ, сбисЗначениеCOM.Object);
				КонецЕсли;
			Исключение
				ОписаниеОшибки = ИнформацияОбОшибке().Описание;
				ВызватьИсключение сбисТекстИсключенияПриКонвертации(Результат.Type, ОписаниеОшибки, КлючИЗначение.Ключ, ВерхнийУровень);
			КонецПопытки;
		КонецЦикла;
	ИначеЕсли ТипЗнч(ЗначениеПреобразовать) = Тип(""Дата"") Тогда
		Результат.Object = Формат(ЗначениеПреобразовать, ""ДФ='дд.ММ.гггг ЧЧ.мм.сс'"");
	ИначеЕсли ТипЗнч(ЗначениеПреобразовать) = Тип(""Число"") Тогда
		Результат.Object = Формат(ЗначениеПреобразовать, ""ЧГ=0"");
	Иначе
		Результат.Object = Строка(ЗначениеПреобразовать);
	КонецЕсли;
	Возврат Результат;	
КонецФункции

#КонецОбласти
",
{4,1,
{9,
{1},0,"Объект",
{1,0},
{"Pattern",
{"#",9b8aff49-ad7e-4a36-8244-27fabb591eac}
},
{0,
{0,
{"B",1},0}
},
{0,
{0,
{"B",1},0}
},
{0,0},
{0,0},1,0,0,0,
{0,0},
{0,0}
},0,0,
{#base64:77u/PD94bWwgdmVyc2lvbj0iMS4wIiBlbmNvZGluZz0iVVRGLTgiPz4NCjxTZXR0
aW5ncyB4bWxucz0iaHR0cDovL3Y4LjFjLnJ1LzguMS9kYXRhLWNvbXBvc2l0aW9u
LXN5c3RlbS9zZXR0aW5ncyIgeG1sbnM6ZGNzY29yPSJodHRwOi8vdjguMWMucnUv
OC4xL2RhdGEtY29tcG9zaXRpb24tc3lzdGVtL2NvcmUiIHhtbG5zOnN0eWxlPSJo
dHRwOi8vdjguMWMucnUvOC4xL2RhdGEvdWkvc3R5bGUiIHhtbG5zOnN5cz0iaHR0
cDovL3Y4LjFjLnJ1LzguMS9kYXRhL3VpL2ZvbnRzL3N5c3RlbSIgeG1sbnM6djg9
Imh0dHA6Ly92OC4xYy5ydS84LjEvZGF0YS9jb3JlIiB4bWxuczp2OHVpPSJodHRw
Oi8vdjguMWMucnUvOC4xL2RhdGEvdWkiIHhtbG5zOndlYj0iaHR0cDovL3Y4LjFj
LnJ1LzguMS9kYXRhL3VpL2NvbG9ycy93ZWIiIHhtbG5zOndpbj0iaHR0cDovL3Y4
LjFjLnJ1LzguMS9kYXRhL3VpL2NvbG9ycy93aW5kb3dzIiB4bWxuczp4cz0iaHR0
cDovL3d3dy53My5vcmcvMjAwMS9YTUxTY2hlbWEiIHhtbG5zOnhzaT0iaHR0cDov
L3d3dy53My5vcmcvMjAwMS9YTUxTY2hlbWEtaW5zdGFuY2UiPg0KCTxvdXRwdXRQ
YXJhbWV0ZXJzLz4NCjwvU2V0dGluZ3M+}
},
{0,0},
{0,0},
{0,0},
{0,0},0,0}